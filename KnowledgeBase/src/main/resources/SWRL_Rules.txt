MotorMaximalSpeedSatisfyRule:
Motor(?m) ^ hasNominalRotationSpeed_n_max_unit_rpm(?m, ?n_motor) ^ CurrentRequirements(?r) ^ hasMaximalSpeedReqMax_n_max_unit_rpm(?r, ?n_req_max) ^ swrlb:divide(?n_res_min, ?n_motor, 160.0) ^ swrlb:lessThanOrEqual(?n_res_min, ?n_req_max) ^ hasMaximalSpeedReqMin_n_max_unit_rpm(?r, ?n_req_min) ^ swrlb:divide(?n_res_max, ?n_motor, 1.0) ^ swrlb:greaterThanOrEqual(?n_res_max, ?n_req_min) -> SatisfiedRotationSpeed(?m)

MotorPeakTorqueSatisfyRule:
Motor(?m) ^ hasNominalTorque_M_n_unit_Nm(?m, ?M_n) ^ CurrentRequirements(?r) ^ hasPeakTorqueReqMax_M_max_unit_Nm(?r, ?M_req_max) ^ swrlb:multiply(?M_res_min, ?M_n, 1.0) ^ swrlb:lessThanOrEqual(?M_res_min, ?M_req_max) ^ swrlb:multiply(?M_res_max, ?M_n, 160.0) ^ hasPeakTorqueReqMin_M_max_unit_Nm(?r, ?M_req_min) ^ swrlb:greaterThanOrEqual(?M_res_max, ?M_req_min) -> SatisfiedPeakTorque(?m)

MotorWeightSatisfyRule:
Motor(?m) ^ hasWeight_m_unit_kg(?m, ?m_m) ^ CurrentRequirements(?r) ^ hasWeightReqMax_m_unit_kg(?r, ?m_req_max) ^ swrlb:add(?m_res_min, ?m_m, 0.0) ^ swrlb:lessThanOrEqual(?m_res_min, ?m_req_max) ^ hasWeightReqMin_m_unit_kg(?r, ?m_req_min) ^ swrlb:add(?m_res_max, ?m_m, 1.9) ^ swrlb:greaterThanOrEqual(?m_res_max, ?m_req_min) -> SatisfiedWeight(?m)


MotorSatisfyRule:
Motor(?m) ^ SatisfiedRotationSpeed(?m) ^ SatisfiedPeakTorque(?m) ^ SatisfiedWeight(?m) -> SatisfiedMotor(?m)



GearBoxMaximalSpeedSatisfyRule:
GearBox(?g) ^ hasGearBoxRation_i(?g, ?i) ^ CurrentRequirements(?r) ^ hasMaximalSpeedReqMax_n_max_unit_rpm(?r, ?n_req_max) ^ swrlb:divide(?n_res_min, 650.0, ?i) ^ swrlb:lessThanOrEqual(?n_res_min, ?n_req_max) ^ hasMaximalSpeedReqMin_n_max_unit_rpm(?r, ?n_req_min) ^ swrlb:divide(?n_res_max, 24000.0, ?i) ^ swrlb:greaterThanOrEqual(?n_res_max, ?n_req_min) ^ hasMaxRotationInputSpeedGrease_n_max_unit_rpm(?g, ?n_max) ^ swrlb:divide(?n_max_res, ?n_max, ?i) ^ swrlb:lessThanOrEqual(?n_res_min, ?n_max_res) ^ swrlb:lessThanOrEqual(?n_req_min, ?n_max_res) -> SatisfiedRotationSpeed(?g)

GearBoxPeakTorqueSatisfyRule:
GearBox(?g) ^ hasGearBoxRation_i(?g, ?i) ^ CurrentRequirements(?r) ^ hasPeakTorqueReqMax_M_max_unit_Nm(?r, ?M_req_max) ^ swrlb:multiply(?M_res_min, 0.024, ?i) ^ swrlb:lessThanOrEqual(?M_res_min, ?M_req_max) ^ swrlb:multiply(?M_res_max, 11.2, ?i) ^ hasPeakTorqueReqMin_M_max_unit_Nm(?r, ?M_req_min) ^ swrlb:greaterThanOrEqual(?M_res_max, ?M_req_min) ^ hasPeakTorque_M_max_unit_Nm(?g, ?M_max) ^ swrlb:lessThanOrEqual(?M_res_min, ?M_max) ^ swrlb:lessThanOrEqual(?M_req_min, ?M_max) -> SatisfiedPeakTorque(?g)

GearBoxWeightSatisfyRule:
GearBox(?g) ^ hasWeight_m_unit_kg(?g, ?m_g) ^ CurrentRequirements(?r) ^ hasWeightReqMax_m_unit_kg(?r, ?m_req_max) ^ swrlb:add(?m_res_min, ?m_g, 0.016) ^ swrlb:lessThanOrEqual(?m_res_min, ?m_req_max) ^ swrlb:add(?m_res_max, ?m_g, 2.17) ^ hasWeightReqMin_m_unit_kg(?r, ?m_req_min) ^ swrlb:greaterThanOrEqual(?m_res_max, ?m_req_min) -> SatisfiedWeight(?g)


GearBoxSatisfyRule:
GearBox(?g) ^ SatisfiedRotationSpeed(?g) ^ SatisfiedPeakTorque(?g) ^ SatisfiedWeight(?g) -> SatisfiedGearBox(?g)




MotorGearBoxMatchMaximalSpeedCalcRule:
MotorGearBoxMatch(?ma) ^ hasMotor(?ma, ?m) ^ hasGearBox(?ma, ?g) ^ hasNominalRotationSpeed_n_max_unit_rpm(?m, ?n_motor) ^ hasGearBoxRation_i(?g, ?i) ^ swrlb:divide(?n_res, ?n_motor, ?i) -> hasMaximalSpeedRes_n_max_unit_rpm(?ma, ?n_res)

MotorGearBoxMatchMaximalSpeedSatisfyRule:
MotorGearBoxMatch(?ma) ^ hasGearBox(?ma, ?g) ^ hasMaxRotationInputSpeedGrease_n_max_unit_rpm(?g, ?n_max) ^ hasGearBoxRation_i(?g, ?i) ^ swrlb:divide(?n_max_res, ?n_max, ?i) ^ hasMaximalSpeedRes_n_max_unit_rpm(?ma, ?n_res) ^ swrlb:lessThanOrEqual(?n_res, ?n_max_res) ^ CurrentRequirements(?r) ^ hasMaximalSpeedReqMax_n_max_unit_rpm(?r, ?n_req_max) ^ swrlb:lessThanOrEqual(?n_res, ?n_req_max) ^ hasMaximalSpeedReqMin_n_max_unit_rpm(?r, ?n_req_min) ^ swrlb:greaterThanOrEqual(?n_res, ?n_req_min) -> SatisfiedRotationSpeed(?ma)


MotorGearBoxMatchPeakTorqueCalcRule:
MotorGearBoxMatch(?ma) ^ hasMotor(?ma, ?m) ^ hasGearBox(?ma, ?g) ^ hasNominalTorque_M_n_unit_Nm(?m, ?M_n) ^ hasGearBoxRation_i(?g, ?i) ^ swrlb:multiply(?M_res, ?M_n, ?i) -> hasPeakTorqueRes_M_max_unit_Nm(?ma, ?M_res)

MotorGearBoxMatchPeakTorqueSatisfyRule:
MotorGearBoxMatch(?ma) ^ hasGearBox(?ma, ?g) ^ hasPeakTorque_M_max_unit_Nm(?g, ?M_max) ^ hasPeakTorqueRes_M_max_unit_Nm(?ma, ?M_res) ^ swrlb:lessThanOrEqual(?M_res, ?M_max) ^ CurrentRequirements(?r) ^ hasPeakTorqueReqMax_M_max_unit_Nm(?r, ?M_req_max) ^ swrlb:lessThanOrEqual(?M_res, ?M_req_max) ^ hasPeakTorqueReqMin_M_max_unit_Nm(?r, ?M_req_min) ^ swrlb:greaterThanOrEqual(?M_res, ?M_req_min) -> SatisfiedPeakTorque(?ma)



MotorGearBoxMatchLengthCalcRule:
MotorGearBoxMatch(?ma) ^ hasMotor(?ma, ?m) ^ hasGearBox(?ma, ?g) ^ hasDimensionLength_L_unit_mm(?m, ?L_m) ^ hasDimensionLength_L_unit_mm(?g, ?L_g) ^ swrlb:add(?L_ges, ?L_m, ?L_g) -> hasDimensionLength_L_unit_mm(?ma, ?L_ges)

MotorGearBoxMatchLengthSatisfyRule:
MotorGearBoxMatch(?ma) ^ hasDimensionLength_L_unit_mm(?ma, ?L_ges) ^ CurrentRequirements(?r) ^ hasDimensionLengthReqMax_L_unit_mm(?r, ?L_req_max) ^ swrlb:lessThanOrEqual(?L_ges, ?L_req_max) ^ hasDimensionLengthReqMin_L_unit_mm(?r, ?L_req_min) ^ swrlb:greaterThanOrEqual(?L_ges, ?L_req_min) -> SatisfiedLength(?ma)


MotorGearBoxMatchOuterDiameter1CalcRule:
MotorGearBoxMatch(?ma) ^ hasMotor(?ma, ?m) ^ hasGearBox(?ma, ?g) ^ hasDimensionOuterDiameter_D_unit_mm(?m, ?D_m) ^ hasDimensionOuterDiameter_D_unit_mm(?g, ?D_g) ^ swrlb:greaterThan(?D_m, ?D_g) -> hasDimensionOuterDiameter_D_unit_mm(?ma, ?D_m)
MotorGearBoxMatchOuterDiameter2CalcRule:
MotorGearBoxMatch(?ma) ^ hasMotor(?ma, ?m) ^ hasGearBox(?ma, ?g) ^ hasDimensionOuterDiameter_D_unit_mm(?m, ?D_m) ^ hasDimensionOuterDiameter_D_unit_mm(?g, ?D_g) ^ swrlb:lessThanOrEqual(?D_m, ?D_g) -> hasDimensionOuterDiameter_D_unit_mm(?ma, ?D_g)

MotorGearBoxMatchOuterDiameterSatisfyRule:
MotorGearBoxMatch(?ma) ^ hasDimensionOuterDiameter_D_unit_mm(?ma, ?D_ges) ^ CurrentRequirements(?r) ^ hasDimensionOuterDiameterReqMax_D_unit_mm(?r, ?D_req_max) ^ swrlb:lessThanOrEqual(?D_ges, ?D_req_max) ^ hasDimensionOuterDiameterReqMin_D_unit_mm(?r, ?D_req_min) ^ swrlb:greaterThanOrEqual(?D_ges, ?D_req_min) -> SatisfiedOuterDiameter(?ma)



MotorGearBoxMatchWeightCalcRule:
MotorGearBoxMatch(?ma) ^ hasMotor(?ma, ?m) ^ hasGearBox(?ma, ?g) ^ hasWeight_m_unit_kg(?m, ?m_m) ^ hasWeight_m_unit_kg(?g, ?m_g) ^ swrlb:add(?m_ges, ?m_m, ?m_g) -> hasWeight_m_unit_kg(?ma, ?m_ges)

MotorGearBoxMatchWeightSatisfyRule:
MotorGearBoxMatch(?ma) ^ hasWeight_m_unit_kg(?ma, ?m_ges) ^ CurrentRequirements(?r) ^ hasWeightReqMax_m_unit_kg(?r, ?m_req_max) ^ swrlb:lessThanOrEqual(?m_ges, ?m_req_max) ^ hasWeightReqMin_m_unit_kg(?r, ?m_req_min) ^ swrlb:greaterThanOrEqual(?m_ges, ?m_req_min) -> SatisfiedWeight(?ma)



MotorGearBoxMatchSatisfyRule:
MotorGearBoxMatch(?ma) ^ SatisfiedRotationSpeed(?ma) ^ SatisfiedPeakTorque(?ma) ^ SatisfiedLength(?ma) ^ SatisfiedOuterDiameter(?ma) ^ SatisfiedWeight(?ma) ^ hasMotor(?ma, ?m) ^ hasGearBox(?ma, ?g) -> SatisfiedMotorGearBoxMatch(?ma) ^ SatisfiedMotorInMotorGearBoxMatch(?m) ^ SatisfiedGearBoxInMotorGearBoxMatch(?g) ^ isComposedOfMotor(?ma, ?m) ^ isComposedOfGearBox(?ma, ?g)





MotorInputBearingMatchSatisfyRule:
MotorInputBearingMatch(?mibm) ^ hasSACunitDiameterInput(?mibm, ?sacdi) ^ hasMotor(?sacdi, ?m) ^ hasInputBearing(?mibm, ?ib) ^ hasDimensionInnerDiameter_d_unit_mm(?m, ?d_m) ^ hasDimensionInnerDiameter_d_unit_mm(?ib, ?d_ib) ^ swrlb:lessThanOrEqual(?d_ib, ?d_m) -> HalfSatisfiedMotorInputBearingMatch(?mibm)


MotorInputBearingMatchUnsatisfiedRule:
HalfSatisfiedMotorInputBearingMatch(?mibm1) ^ hasSACunitDiameterInput(?mibm1, ?sacdi1) ^ hasMotor(?sacdi1, ?m) ^ HalfSatisfiedMotorInputBearingMatch(?mibm2) ^ hasSACunitDiameterInput(?mibm2, ?sacdi2) ^ hasMotor(?sacdi2, ?m) ^ hasInputBearing(?mibm1, ?ib1) ^ hasInputBearing(?mibm2, ?ib2) ^ hasDimensionInnerDiameter_d_unit_mm(?ib1, ?d_ib1) ^ hasDimensionInnerDiameter_d_unit_mm(?ib2, ?d_ib2) ^ swrlb:greaterThan(?d_ib1, ?d_ib2) ^ swrlb:lessThan(?d_ib2, ?d_ib1) -> UnsatisfiedMotorInputBearingMatch(?mibm2)

MotorInputBearingMatchPossibleSatisfiedRule:
HalfSatisfiedMotorInputBearingMatch(?mibm1) ^ hasSACunitDiameterInput(?mibm1, ?sacdi1) ^ hasMotor(?sacdi1, ?m) ^ HalfSatisfiedMotorInputBearingMatch(?mibm2) ^ hasSACunitDiameterInput(?mibm2, ?sacdi2) ^ hasMotor(?sacdi2, ?m) ^ hasInputBearing(?mibm1, ?ib1) ^ hasInputBearing(?mibm2, ?ib2) ^ hasDimensionInnerDiameter_d_unit_mm(?ib1, ?d_ib1) ^ hasDimensionInnerDiameter_d_unit_mm(?ib2, ?d_ib2) ^ swrlb:equal(?d_ib2, ?d_ib1) -> PossibleSatisfiedMotorInputBearingMatch(?mibm1) ^ PossibleSatisfiedMotorInputBearingMatch(?mibm2)




IncrementalEncoderSatisfy1Rule:
NoIE(?ie) ^ CurrentRequirements(?r) ^ needIncrementalEncoderReq(?r, ?ier) ^ swrlb:equal(?ier, false) -> SatisfiedIncrementalEncoder(?ie)

IncrementalEncoderSatisfy2Rule:
WithIE(?ie) ^ CurrentRequirements(?r) ^ needIncrementalEncoderReq(?r, ?ier) ^ swrlb:equal(?ier, true) -> SatisfiedIncrementalEncoder(?ie)




SACunitLengthInputLinearCalcRule:
SACunitLengthInputLinear(?saclil) ^ hasMotorInputBearingMatch(?saclil, ?mibm) ^ hasIncrementalEncoder(?saclil, ?ie) ^ hasGearBox(?saclil, ?g) ^ hasInputBearing(?mibm, ?ib) ^ hasSACunitDiameterInput(?mibm, ?sacdi) ^ hasMotor(?sacdi, ?m) ^ hasMotorController(?sacdi, ?mc) ^ SatisfiedMotorGearBoxMatch(?ma) ^ hasMotor(?ma, ?m) ^ hasGearBox(?ma, ?g) ^ hasDimensionLength_L_unit_mm(?ie, ?L_ie) ^ hasDimensionLength_L_unit_mm(?m, ?L_m) ^ hasDimensionLength_L_unit_mm(?ib, ?L_ib) ^ hasDimensionLength_L_unit_mm(?mc, ?L_mc) ^ Constants(?con) ^ lengthFloatBearingOffset(?con, ?fbo_con) ^ lengthMotorCover(?con, ?mc_con) ^ lengthMotorWiring(?con, ?mw_con) ^ swrlb:multiply(?L_fixedB, ?L_ib, 2.0) ^ swrlb:add(?L_temp1, ?L_m, ?L_fixedB) ^ hasDimensionSafety_c_unit_mm(?g, ?c_g) ^ swrlb:add(?L_temp2, ?L_temp1, ?c_g) ^ swrlb:add(?L_1, ?L_temp2, ?L_mc) ^ swrlb:add(?L_floatB, ?L_ib, ?fbo_con) ^ swrlb:add(?L_max1, ?L_floatB, ?L_ie) ^ swrlb:add(?L_max2, ?mc_con, ?mw_con) -> has1(?saclil, ?L_1) ^ hasMax1_1(?saclil, ?L_max1) ^ hasMax1_2(?saclil, ?L_max2) ^ SatisfiedSACunitLengthInputLinear(?saclil)




SACunitLengthInputLinearEvaluate1Rule:
SatisfiedSACunitLengthInputLinear(?saclil) ^ has1(?saclil, ?L_1) ^ hasMax1_1(?saclil, ?L_max1) ^ hasMax1_2(?saclil, ?L_max2) ^ swrlb:greaterThan(?L_max1, ?L_max2) ^ swrlb:add(?L_ges, ?L_1, ?L_max1) -> hasDimensionLength_L_unit_mm(?saclil, ?L_ges)

SACunitLengthInputLinearEvaluate2Rule:
SatisfiedSACunitLengthInputLinear(?saclil) ^ has1(?saclil, ?L_1) ^ hasMax1_1(?saclil, ?L_max1) ^ hasMax1_2(?saclil, ?L_max2) ^ swrlb:lessThanOrEqual(?L_max1, ?L_max2) ^ swrlb:add(?L_ges, ?L_1, ?L_max2) -> hasDimensionLength_L_unit_mm(?saclil, ?L_ges)





AbsoluteEncoderSensorPCBMatchSatisfy1Rule:
sac_basic:NoAeNoPCB(?aespcbm) ^ CurrentRequirements(?r) ^ needAbsoluteEncoderReq(?r, ?ab) ^ swrlb:equal(?ab, false) ^ needSensorADC_PCM_TorqueReq(?r, ?spcm) ^ swrlb:equal(?spcm, false) -> SatisfiedAbsoluteEncoderSensorPCBMatch(?aespcbm)

AbsoluteEncoderSensorPCBMatchSatisfy2Rule:
sac_basic:NoAeWithPCB(?aespcbm) ^ CurrentRequirements(?r) ^ needAbsoluteEncoderReq(?r, ?ab) ^ swrlb:equal(?ab, false) ^ needSensorADC_PCM_TorqueReq(?r, ?spcm) ^ swrlb:equal(?spcm, true) -> SatisfiedAbsoluteEncoderSensorPCBMatch(?aespcbm)

AbsoluteEncoderSensorPCBMatchSatisfy3Rule:
sac_basic:WithAeNoPCB(?aespcbm) ^ CurrentRequirements(?r) ^ needAbsoluteEncoderReq(?r, ?ab) ^ swrlb:equal(?ab, true) ^ needSensorADC_PCM_TorqueReq(?r, ?spcm) ^ swrlb:equal(?spcm, false) -> SatisfiedAbsoluteEncoderSensorPCBMatch(?aespcbm)

AbsoluteEncoderSensorPCBMatchSatisfy4Rule:
sac_basic:WithAeWithPCB(?aespcbm) ^ CurrentRequirements(?r) ^ needAbsoluteEncoderReq(?r, ?ab) ^ swrlb:equal(?ab, true) ^ needSensorADC_PCM_TorqueReq(?r, ?spcm) ^ swrlb:equal(?spcm, true) -> SatisfiedAbsoluteEncoderSensorPCBMatch(?aespcbm)





GearBoxScrewMatchSatisfyRule:
GearBoxScrewMatch(?gs) ^ hasSACunitDiameterOutput(?gs, ?sacdo) ^ hasScrew(?gs, ?s) ^ hasGearBox(?sacdo, ?g) ^ hasDimensionScrewInnerDiameterFlexSplineM_d_unit_mm(?g, ?d_s1) ^ hasDimensionScrewInnerDiameterM_d_unit_mm(?s, ?d_s2) ^ swrlb:equal(?d_s1, ?d_s2) -> SatisfiedGearBoxScrewMatch(?gs)


OutputBearingScrewMatchSatisfyRule:
OutputBearingScrewMatch(?obs) ^ hasOutputBearing(?obs, ?ob) ^ hasScrew(?obs, ?s) ^ hasDimensionScrewInnerDiameterM_d_unit_mm(?ob, ?d_s1) ^ hasDimensionScrewInnerDiameterM_d_unit_mm(?s, ?d_s2) ^ swrlb:equal(?d_s1, ?d_s2) -> SatisfiedOutputBearingScrewMatch(?obs)




SACunitLengthOutputLinearCalcRule:
SACunitLengthOutputLinear(?saclol) ^ hasGearBoxScrewMatch(?saclol, ?gs) ^ hasOutputBearingScrewMatch(?saclol, ?obs) ^ hasSACunitDiameterOutput(?gs, ?sacdo) ^ hasAbsoluteEncoderSensorPCBMatch(?sacdo, ?aespcbm) ^ hasGearBox(?sacdo, ?g) ^ hasScrew(?gs, ?s_gs) ^ hasOutputBearing(?obs, ?ob) ^ hasScrew(?obs, ?s_ob) ^ hasDimensionLength_L_unit_mm(?g, ?L_g) ^ hasDimensionLengthScrewHead_k_unit_mm(?s_gs, ?L_s_gs) ^ hasDimensionScrewInnerDiameterM_d_unit_mm(?s_gs, ?d_s_gs) ^ hasDimensionLength_L_unit_mm(?ob, ?L_ob) ^ hasDimensionLengthScrewHead_k_unit_mm(?s_ob, ?L_s_ob) ^ hasDimensionLength_L_unit_mm(?aespcbm, ?L_loie) ^ Constants(?con) ^ lengthADC(?con, ?adc_con) ^ lengthCentering(?con, ?c_con) ^ lengthNotch(?con, ?n_con) ^ lengthMeasurementWidth(?con, ?mw_con) ^ swrlb:multiply(?L_2kerbe, 2.0, ?n_con) ^ swrlb:add(?L_torWelleTemp1, ?mw_con, ?L_2kerbe) ^ swrlb:multiply(?L_torWelleTemp2, ?d_s_gs, 1.5) ^ swrlb:multiply(?L_torWelleTemp3, 2.0, ?L_torWelleTemp2) ^ swrlb:add(?L_torWelle, ?L_torWelleTemp1, ?L_torWelleTemp3) ^ swrlb:add(?L_max1_1, ?L_torWelle, ?L_s_gs) ^ swrlb:multiply(?L_abLagerTemp1, 1.5, ?L_ob) ^ swrlb:add(?L_abLager, ?L_abLagerTemp1, ?c_con) ^ swrlb:add(?L_max1_2, ?L_loie, ?L_abLager) ^ swrlb:add(?L_max2_1, ?adc_con, ?L_s_gs) ^ swrlb:multiply(?L_max2_2, 2.0, ?L_s_ob) -> hasMax1_1(?saclol, ?L_max1_1) ^ hasMax1_2(?saclol, ?L_max1_2) ^ hasMax2_1(?saclol, ?L_max2_1) ^ hasMax2_2(?saclol, ?L_max2_2) ^ has1(?saclol, ?L_g)


SACunitLengthOutputLinearEvaluate1Rule:
SACunitLengthOutputLinear(?saclol) ^ hasMax1_1(?saclol, ?L_max1_1) ^ hasMax1_2(?saclol, ?L_max1_2) ^ swrlb:greaterThan(?L_max1_1, ?L_max1_2) -> has2(?saclol, ?L_max1_1)

SACunitLengthOutputLinearEvaluate2Rule:
SACunitLengthOutputLinear(?saclol) ^ hasMax1_1(?saclol, ?L_max1_1) ^ hasMax1_2(?saclol, ?L_max1_2) ^ swrlb:lessThanOrEqual(?L_max1_1, ?L_max1_2) -> has2(?saclol, ?L_max1_2)

SACunitLengthOutputLinearEvaluate3Rule:
SACunitLengthOutputLinear(?saclol) ^ has1(?saclol, ?L_part1) ^ has2(?saclol, ?L_part2) ^ hasMax2_1(?saclol, ?L_max2_1) ^ hasMax2_2(?saclol, ?L_max2_2) ^ swrlb:greaterThan(?L_max2_1, ?L_max2_2) ^ swrlb:add(?L_ges_temp, ?L_part1, ?L_part2) ^ swrlb:add(?L_ges, ?L_ges_temp, ?L_max2_1) -> hasDimensionLength_L_unit_mm(?saclol, ?L_ges) ^ SatisfiedSACunitLengthOutputLinear(?saclol)

SACunitLengthOutputLinearEvaluate4Rule:
SACunitLengthOutputLinear(?saclol) ^ has1(?saclol, ?L_part1) ^ has2(?saclol, ?L_part2) ^ hasMax2_1(?saclol, ?L_max2_1) ^ hasMax2_2(?saclol, ?L_max2_2) ^ swrlb:lessThanOrEqual(?L_max2_1, ?L_max2_2) ^ swrlb:add(?L_ges_temp, ?L_part1, ?L_part2) ^ swrlb:add(?L_ges, ?L_ges_temp, ?L_max2_2) -> hasDimensionLength_L_unit_mm(?saclol, ?L_ges) ^ SatisfiedSACunitLengthOutputLinear(?saclol)



SACunitLengthCalcRule:
SACunitLength(?sacl) ^ hasSACunitLengthInput(?sacl, ?sacli) ^ hasSACunitLengthOutput(?sacl, ?saclo) ^ hasGearBox(?sacli, ?g) ^ hasGearBoxScrewMatch(?saclo, ?gs) ^ hasSACunitDiameterOutput(?gs, ?sacdo) ^ hasGearBox(?sacdo, ?g) ^ hasSACunitLengthElectric(?sacl, ?sacle) ^ hasDimensionLength_L_unit_mm(?sacli, ?L_sacli) ^ hasDimensionLength_L_unit_mm(?saclo, ?L_saclo) ^ hasDimensionLength_L_unit_mm(?sacle, ?L_sacle) ^ swrlb:add(?L_temp1, ?L_sacli, ?L_saclo) ^ swrlb:add(?L_ges, ?L_temp1, ?L_sacle) -> hasDimensionLength_L_unit_mm(?sacl, ?L_ges)


SACunitLengthSatisfyRule:
SACunitLength(?sacl) ^ hasDimensionLength_L_unit_mm(?sacl, ?L_ges) ^ CurrentRequirements(?r) ^ hasDimensionLengthReqMax_L_unit_mm(?r, ?L_req_max) ^ swrlb:lessThanOrEqual(?L_ges, ?L_req_max) ^ hasDimensionLengthReqMin_L_unit_mm(?r, ?L_req_min) ^ swrlb:greaterThanOrEqual(?L_ges, ?L_req_min) -> SatisfiedSACunitLength(?sacl)






SACunitDiameterInputCalcRule:
SACunitDiameterInput(?sacdi) ^ hasMotor(?sacdi, ?m) ^ hasMotorController(?sacdi, ?mc) ^ hasDimensionOuterDiameter_D_unit_mm(?m, ?D_m) ^ hasDimensionOuterDiameter_D_unit_mm(?mc, ?D_mc) ^ CurrentRequirements(?r) ^ hasThicknessEngineCaseReq_D_unit_mm(?r, ?tec_r) ^ swrlb:multiply(?dec, ?tec_r, 2.0) ^ swrlb:multiply(?dmc, ?D_mc, 2.0) ^ swrlb:add(?temp, ?dec, ?dmc) ^ swrlb:add(?d_input, ?temp, ?D_m) -> hasDimensionOuterDiameter_D_unit_mm(?sacdi, ?D_m)



SACunitDiameterInputSatisfyRule:
SACunitDiameterInput(?sacdi) ^ hasDimensionOuterDiameter_D_unit_mm(?sacdi, ?D_input) ^ CurrentRequirements(?r) ^ hasDimensionOuterDiameterReqMax_D_unit_mm(?r, ?D_req_max) ^ swrlb:lessThanOrEqual(?D_input, ?D_req_max) -> SatisfiedSACunitDiameterInput(?sacdi)




SACunitDiameterOutputCalcRule:
SACunitDiameterOutput(?sacdo) ^ hasGearBox(?sacdo, ?g) ^ hasAbsoluteEncoderSensorPCBMatch(?sacdo, ?aespcbm) ^ Constants(?con) ^ hasDimensionOuterDiameter_D_unit_mm(?g, ?D_g) ^ diameterWiring(?con, ?D_w) ^ hasDimensionScrewInnerDiameterCircularSplineM_d_unit_mm(?g, ?D_scp) ^ hasDimensionOuterDiameter_D_unit_mm(?aespcbm, ?D_aespcbm) ^ diameterElectronicsCover(?con, ?D_ec) ^ swrlb:multiply(?D_w2, ?D_w, 2.0) ^ swrlb:multiply(?D_scp2, ?D_scp, 2.0) ^ swrlb:add(?temp1, ?D_g, ?D_w2) ^ swrlb:add(?D_max1, ?temp1, ?D_scp2) ^ swrlb:multiply(?D_ec2, ?D_ec, 2.0) ^ swrlb:add(?D_max2, ?D_ec2, ?D_aespcbm) -> hasMax1_1(?sacdo, ?D_max1) ^ hasMax1_2(?sacdo, ?D_max2)



SACunitDiameterOutputEvaluate1Rule:
SACunitDiameterOutput(?sacdo) ^ hasMax1_1(?sacdo, ?D_max1) ^ hasMax1_2(?sacdo, ?D_max2) ^ swrlb:greaterThan(?D_max1, ?D_max2) -> hasDimensionOuterDiameter_D_unit_mm(?sacdo, ?D_max1)

SACunitDiameterOutputEvaluate2Rule:
SACunitDiameterOutput(?sacdo) ^ hasMax1_1(?sacdo, ?D_max1) ^ hasMax1_2(?sacdo, ?D_max2) ^ swrlb:lessThanOrEqual(?D_max1, ?D_max2) -> hasDimensionOuterDiameter_D_unit_mm(?sacdo, ?D_max2)



SACunitDiameterOutputSatisfyRule:
SACunitDiameterOutput(?sacdo) ^ hasDimensionOuterDiameter_D_unit_mm(?sacdo, ?D_out) ^ CurrentRequirements(?r) ^ hasDimensionOuterDiameterReqMax_D_unit_mm(?r, ?D_req_max) ^ swrlb:lessThanOrEqual(?D_out, ?D_req_max) -> SatisfiedSACunitDiameterOutput(?sacdo)







SACunitLengthMotorGearBoxMatchSatisfyRule:
SACunit(?sac) ^ hasSACunitLength(?sac, ?sacl) ^ hasSACunitLengthInput(?sacl, ?sacli) ^ hasGearBox(?sacli, ?g) ^ hasMotorInputBearingMatch(?sacli, ?mibm) ^ hasSACunitDiameterInput(?mibm, ?sacdi) ^ hasMotor(?sacdi, ?m) ^ SatisfiedMotorGearBoxMatch(?ma) ^ hasMotor(?ma, ?m) ^ hasGearBox(?ma, ?g) ^ hasDimensionLength_L_unit_mm(?sacl, ?L_res) ^ hasMaximalSpeedRes_n_max_unit_rpm(?ma, ?n_res) ^ hasPeakTorqueRes_M_max_unit_Nm(?ma, ?M_res) ^ hasDimensionOuterDiameter_D_unit_mm(?ma, ?D_g) ^ hasWeight_m_unit_kg(?ma, ?m_ges) -> SatisfiedRotationSpeed(?sac) ^ hasMaximalSpeedRes_n_max_unit_rpm(?sac, ?n_res) ^ SatisfiedPeakTorque(?sac) ^ hasPeakTorqueRes_M_max_unit_Nm(?sac, ?M_res) ^ SatisfiedOuterDiameter(?sac) ^ hasDimensionOuterDiameter_D_unit_mm(?sac, ?D_g) ^ SatisfiedWeight(?sac) ^ hasWeight_m_unit_kg(?sac, ?m_ges) ^ SatisfiedLength(?sac) ^ hasDimensionLength_L_unit_mm(?sac, ?L_res)


SACunitSatisfyRule:
SACunit(?sac) ^ SatisfiedRotationSpeed(?sac) ^ SatisfiedPeakTorque(?sac) ^ SatisfiedLength(?sac) ^ SatisfiedOuterDiameter(?sac) ^ SatisfiedWeight(?sac) -> SatisfiedSACunit(?sac)


SACunitIsComposedOfRule:
SatisfiedSACunit(?sac) ^ hasSACunitLength(?sac, ?sacl) ^ hasSACunitLengthInput(?sacl, ?sacli) ^ hasMotorInputBearingMatch(?sacli, ?mibm) ^ hasSACunitDiameterInput(?mibm, ?sacdi) ^ hasMotorController(?sacdi, ?mc) ^ hasIncrementalEncoder(?saclil, ?ie) ^ hasMotor(?sacdi, ?m) ^ hasGearBox(?sacli, ?g) ^ hasSACunitLengthOutput(?sacl, ?saclo) ^ hasGearBoxScrewMatch(?saclo, ?gs) ^ hasScrew(?gs, ?s_gs) ^ hasSACunitDiameterOutput(?gs, ?sacdo) ^ hasInputBearing(?mibm, ?ib) ^ hasSACunitLengthElectric(?sacl, ?sacle) ^ hasAbsoluteEncoderSensorPCBMatch(?sacdo, ?aespcbm) ^ hasOutputBearingScrewMatch(?saclo, ?obs) ^  hasOutputBearing(?obs, ?ob) ^ hasScrew(?obs, ?s_ob) -> isComposedOfIncrementalEncoder(?sac, ?ie) ^ isComposedOfMotor(?sac, ?m) ^ isComposedOfGearBox(?sac, ?g) ^ GearBoxFlexScrew(?s_gs) ^  isComposedOfGearBoxFlexScrew(?sac, ?s_gs) ^ isComposedOfInputBearing(?sac, ?ib) ^ isComposedOfLengthElectric(?sac, ?sacle) ^ isComposedOfAbsoluteEncoderSensorPCB(?sac, ?aespcbm) ^ isComposedOfOutputBearing(?sac, ?ob) ^ OutputBearingScrew(?s_ob) ^ isComposedOfOutputBearingScrew(?sac, ?s_ob) ^ isComposedOfMotorController(?sac, ?mc)



