MotorMaximalSpeedSatisfyRule:
Motor(?m) ^ hasNominalRotationSpeed_n_max_unit_rpm(?m, ?n_motor) ^ CurrentRequirements(?r) ^ hasMaximalSpeedReqMax_n_max_unit_rpm(?r, ?n_req_max) ^ swrlb:divide(?n_res_min, ?n_motor, 160.0) ^ swrlb:lessThanOrEqual(?n_res_min, ?n_req_max) ^ hasMaximalSpeedReqMin_n_max_unit_rpm(?r, ?n_req_min) ^ swrlb:divide(?n_res_max, ?n_motor, 1.0) ^ swrlb:greaterThanOrEqual(?n_res_max, ?n_req_min) -> SatisfiedRotationSpeed(?m)

MotorPeakTorqueSatisfyRule:
Motor(?m) ^ hasNominalTorque_M_n_unit_Nm(?m, ?M_n) ^ CurrentRequirements(?r) ^ hasPeakTorqueReqMax_M_max_unit_Nm(?r, ?M_req_max) ^ swrlb:multiply(?M_res_min, ?M_n, 1.0) ^ swrlb:lessThanOrEqual(?M_res_min, ?M_req_max) ^ swrlb:multiply(?M_res_max, ?M_n, 160.0) ^ hasPeakTorqueReqMin_M_max_unit_Nm(?r, ?M_req_min) ^ swrlb:greaterThanOrEqual(?M_res_max, ?M_req_min) -> SatisfiedPeakTorque(?m)


MotorSatisfyRule:
Motor(?m) ^ SatisfiedRotationSpeed(?m) ^ SatisfiedPeakTorque(?m) -> SatisfiedMotor(?m)



GearBoxMaximalSpeedSatisfyRule:
GearBox(?g) ^ hasGearBoxRation_i(?g, ?i) ^ CurrentRequirements(?r) ^ hasMaximalSpeedReqMax_n_max_unit_rpm(?r, ?n_req_max) ^ swrlb:divide(?n_res_min, 650.0, ?i) ^ swrlb:lessThanOrEqual(?n_res_min, ?n_req_max) ^ hasMaximalSpeedReqMin_n_max_unit_rpm(?r, ?n_req_min) ^ swrlb:divide(?n_res_max, 24000.0, ?i) ^ swrlb:greaterThanOrEqual(?n_res_max, ?n_req_min) ^ hasMaxRotationInputSpeedGrease_n_max_unit_rpm(?g, ?n_max) ^ swrlb:divide(?n_max_res, ?n_max, ?i) ^ swrlb:greaterThanOrEqual(?n_max_res, ?n_req_min) ^ swrlb:lessThanOrEqual(?n_max_res, ?n_req_max) -> SatisfiedRotationSpeed(?g)

GearBoxPeakTorqueSatisfyRule:
GearBox(?g) ^ hasGearBoxRation_i(?g, ?i) ^ CurrentRequirements(?r) ^ hasPeakTorqueReqMax_M_max_unit_Nm(?r, ?M_req_max) ^ swrlb:multiply(?M_res_min, 0.024, ?i) ^ swrlb:lessThanOrEqual(?M_res_min, ?M_req_max) ^ swrlb:multiply(?M_res_max, 11.2, ?i) ^ hasPeakTorqueReqMin_M_max_unit_Nm(?r, ?M_req_min) ^ swrlb:greaterThanOrEqual(?M_res_max, ?M_req_min) ^ hasPeakTorque_M_max_unit_Nm(?g, ?M_max) ^ swrlb:lessThanOrEqual(?M_res_min, ?M_max) ^ swrlb:lessThanOrEqual(?M_req_min, ?M_max) -> SatisfiedPeakTorque(?g)


GearBoxSatisfyRule:
GearBox(?g) ^ SatisfiedRotationSpeed(?g) ^ SatisfiedPeakTorque(?g) -> SatisfiedGearBox(?g)




MotorGearBoxMatchMaximalSpeedCalcRule:
MotorGearBoxMatch(?ma) ^ hasMotor(?ma, ?m) ^ hasGearBox(?ma, ?g) ^ hasNominalRotationSpeed_n_max_unit_rpm(?m, ?n_motor) ^ hasGearBoxRation_i(?g, ?i) ^ swrlb:divide(?n_res, ?n_motor, ?i) -> hasMaximalSpeedRes_n_max_unit_rpm(?ma, ?n_res)

MotorGearBoxMatchMaximalSpeedSatisfyRule:
MotorGearBoxMatch(?ma) ^ hasGearBox(?ma, ?g) ^ hasMaxRotationInputSpeedGrease_n_max_unit_rpm(?g, ?n_max) ^ hasGearBoxRation_i(?g, ?i) ^ swrlb:divide(?n_max_res, ?n_max, ?i) ^ hasMaximalSpeedRes_n_max_unit_rpm(?ma, ?n_res) ^ CurrentRequirements(?r) ^ hasMaximalSpeedReqMax_n_max_unit_rpm(?r, ?n_req_max) ^ swrlb:lessThanOrEqual(?n_res, ?n_req_max) ^ hasMaximalSpeedReqMin_n_max_unit_rpm(?r, ?n_req_min) ^ swrlb:greaterThanOrEqual(?n_res, ?n_req_min) ^ swrlb:lessThanOrEqual(?n_max_res, ?n_req_max) ^ swrlb:greaterThanOrEqual(?n_max_res, ?n_req_min) -> SatisfiedRotationSpeed(?ma)


MotorGearBoxMatchPeakTorqueCalcRule:
MotorGearBoxMatch(?ma) ^ hasMotor(?ma, ?m) ^ hasGearBox(?ma, ?g) ^ hasNominalTorque_M_n_unit_Nm(?m, ?M_n) ^ hasGearBoxRation_i(?g, ?i) ^ swrlb:multiply(?M_res, ?M_n, ?i) -> hasPeakTorqueRes_M_max_unit_Nm(?ma, ?M_res)

MotorGearBoxMatchPeakTorqueSatisfyRule:
MotorGearBoxMatch(?ma) ^ hasGearBox(?ma, ?g) ^ hasPeakTorque_M_max_unit_Nm(?g, ?M_max) ^ hasPeakTorqueRes_M_max_unit_Nm(?ma, ?M_res) ^ swrlb:lessThanOrEqual(?M_res, ?M_max) ^ CurrentRequirements(?r) ^ hasPeakTorqueReqMax_M_max_unit_Nm(?r, ?M_req_max) ^ swrlb:lessThanOrEqual(?M_res, ?M_req_max) ^ hasPeakTorqueReqMin_M_max_unit_Nm(?r, ?M_req_min) ^ swrlb:greaterThanOrEqual(?M_res, ?M_req_min) -> SatisfiedPeakTorque(?ma)



MotorGearBoxMatchLengthCalcRule:
MotorGearBoxMatch(?ma) ^ hasMotor(?ma, ?m) ^ hasGearBox(?ma, ?g) ^ hasDimensionLength_L_unit_mm(?m, ?L_m) ^ hasDimensionLength_L_unit_mm(?g, ?L_g) ^ swrlb:add(?L_ges, ?L_m, ?L_g) -> hasDimensionLength_L_unit_mm(?ma, ?L_ges)

MotorGearBoxMatchLengthSatisfyRule:
MotorGearBoxMatch(?ma) ^ hasDimensionLength_L_unit_mm(?ma, ?L_ges) ^ CurrentRequirements(?r) ^ hasDimensionLengthReqMax_L_unit_mm(?r, ?L_req_max) ^ swrlb:lessThanOrEqual(?L_ges, ?L_req_max) ^ hasDimensionLengthReqMin_L_unit_mm(?r, ?L_req_min) ^ swrlb:greaterThanOrEqual(?L_ges, ?L_req_min) -> SatisfiedDimensionLength(?ma)

MotorGearBoxMatchLengthMaxSatisfyRule:
MotorGearBoxMatch(?ma) ^ hasDimensionLength_L_unit_mm(?ma, ?L_ges) ^ CurrentRequirements(?r) ^ hasDimensionLengthReqMax_L_unit_mm(?r, ?L_req_max) ^ swrlb:lessThanOrEqual(?L_ges, ?L_req_max) -> SatisfiedDimensionLengthMax(?ma)



MotorGearBoxMatchOuterDiameter1CalcRule:
MotorGearBoxMatch(?ma) ^ hasMotor(?ma, ?m) ^ hasGearBox(?ma, ?g) ^ hasDimensionOuterDiameter_D_unit_mm(?m, ?D_m) ^ hasDimensionOuterDiameter_D_unit_mm(?g, ?D_g) ^ swrlb:greaterThan(?D_m, ?D_g) -> hasDimensionOuterDiameter_D_unit_mm(?ma, ?D_m)
MotorGearBoxMatchOuterDiameter2CalcRule:
MotorGearBoxMatch(?ma) ^ hasMotor(?ma, ?m) ^ hasGearBox(?ma, ?g) ^ hasDimensionOuterDiameter_D_unit_mm(?m, ?D_m) ^ hasDimensionOuterDiameter_D_unit_mm(?g, ?D_g) ^ swrlb:lessThanOrEqual(?D_m, ?D_g) -> hasDimensionOuterDiameter_D_unit_mm(?ma, ?D_g)

MotorGearBoxMatchOuterDiameterSatisfyRule:
MotorGearBoxMatch(?ma) ^ hasDimensionOuterDiameter_D_unit_mm(?ma, ?D_ges) ^ CurrentRequirements(?r) ^ hasDimensionOuterDiameterReqMax_D_unit_mm(?r, ?D_req_max) ^ swrlb:lessThanOrEqual(?D_ges, ?D_req_max) ^ hasDimensionOuterDiameterReqMin_D_unit_mm(?r, ?D_req_min) ^ swrlb:greaterThanOrEqual(?D_ges, ?D_req_min) -> SatisfiedOuterDiameter(?ma)

MotorGearBoxMatchOuterDiameterMaxSatisfyRule:
MotorGearBoxMatch(?ma) ^ hasDimensionOuterDiameter_D_unit_mm(?ma, ?D_ges) ^ CurrentRequirements(?r) ^ hasDimensionOuterDiameterReqMax_D_unit_mm(?r, ?D_req_max) ^ swrlb:lessThanOrEqual(?D_ges, ?D_req_max) -> SatisfiedOuterDiameterMax(?ma)



MotorGearBoxMatchWeightCalcRule:
MotorGearBoxMatch(?ma) ^ hasMotor(?ma, ?m) ^ hasGearBox(?ma, ?g) ^ hasWeight_m_unit_kg(?m, ?m_m) ^ hasWeight_m_unit_kg(?g, ?m_g) ^ swrlb:add(?m_ges, ?m_m, ?m_g) -> hasWeight_m_unit_kg(?ma, ?m_ges)

MotorGearBoxMatchWeightSatisfyRule:
MotorGearBoxMatch(?ma) ^ hasWeight_m_unit_kg(?ma, ?m_ges) ^ CurrentRequirements(?r) ^ hasWeightReqMax_m_unit_kg(?r, ?m_req_max) ^ swrlb:lessThanOrEqual(?m_ges, ?m_req_max) ^ hasWeightReqMin_m_unit_kg(?r, ?m_req_min) ^ swrlb:greaterThanOrEqual(?m_ges, ?m_req_min) -> SatisfiedWeight(?ma)

MotorGearBoxMatchWeightMaxSatisfyRule:
MotorGearBoxMatch(?ma) ^ hasWeight_m_unit_kg(?ma, ?m_ges) ^ CurrentRequirements(?r) ^ hasWeightReqMax_m_unit_kg(?r, ?m_req_max) ^ swrlb:lessThanOrEqual(?m_ges, ?m_req_max) -> SatisfiedWeightMax(?ma)



MotorGearBoxMatchSatisfyRule:
MotorGearBoxMatch(?ma) ^ SatisfiedRotationSpeed(?ma) ^ SatisfiedPeakTorque(?ma) ^ SatisfiedDimensionLength(?ma) ^ SatisfiedOuterDiameter(?ma) ^ SatisfiedWeight(?ma) ^ hasMotor(?ma, ?m) ^ hasGearBox(?ma, ?g)
-> SatisfiedMotorGearBoxMatch(?ma) ^ isComposedOfMotor(?ma, ?m) ^ isComposedOfGearBox(?ma, ?g)

MotorGearBoxMatchForSACunitSatisfyRule:
MotorGearBoxMatch(?ma) ^ SatisfiedRotationSpeed(?ma) ^ SatisfiedPeakTorque(?ma) ^ SatisfiedDimensionLengthMax(?ma) ^ SatisfiedOuterDiameterMax(?ma) ^ SatisfiedWeightMax(?ma) ^ hasMotor(?ma, ?m) ^ hasGearBox(?ma, ?g)
-> SatisfiedMotorGearBoxMatchForSACunit(?ma) ^ SatisfiedMotorInMotorGearBoxMatch(?m) ^ SatisfiedGearBoxInMotorGearBoxMatch(?g)



// falls unterschiedliche MC -> MC und MCPosition mit MCMCPositionMatch
MotorControllerRadialSatisfyRule:
MCradial(?mc) ^ CurrentRequirements(?r) ^ needMotorControllerReq(?r, ?nmc) ^ swrlb:equal(?nmc, true) ^ Constants(?con) ^ 
lengthWiringConduit(?con, ?L_wc) ^ diameterElectronicsCover(?con, ?L_ec) ^ swrlb:add(?L_ges, ?L_wc, ?L_ec)
-> SatisfiedMotorController(?mc) ^ hasDimensionLengthElectric_L_uni_mm(?mc, ?L_ges)

MotorControllerFrontSatisfyRule:
MCfront(?mc) ^ CurrentRequirements(?r) ^ needMotorControllerReq(?r, ?nmc) ^ swrlb:equal(?nmc, true) ^ 
hasDimensionLength_L_unit_mm(?mc, ?L_mc)
-> SatisfiedMotorController(?mc) ^ hasDimensionLengthElectric_L_uni_mm(?mc, ?L_mc)

MotorControllerNoneSatisfyRule:
NoMC(?mc) ^ CurrentRequirements(?r) ^ needMotorControllerReq(?r, ?nmc) ^ swrlb:equal(?nmc, false) ^
-> SatisfiedMotorController(?mc) ^ hasDimensionLengthElectric_L_uni_mm(?mc, 0.0)



TorsionalWaveWithSatisfyRule:
WithTorsionalWave(?tw) ^ CurrentRequirements(?r) ^ needTorqueSensorStrainGaugeBasedReq(?r, ?ntssgb) ^ swrlb:equal(?ntssgb, true) -> SatisfiedTorsionalWave(?tw)

TorsionalWaveNoneSatisfyRule:
NoTorsionalWave(?tw) ^ CurrentRequirements(?r) ^ needTorqueSensorStrainGaugeBasedReq(?r, ?ntssgb) ^ swrlb:equal(?ntssgb, false) -> SatisfiedTorsionalWave(?tw)



SlipRingNoneSatisfyRule:
NoSlipRing(?sr) ^ CurrentRequirements(?r) ^ needContinuousRotationReq(?r, ?ncr) ^ swrlb:equal(?ncr, false) ^ hasDimensionDiameterWiringReq_D_unit_mm(?r, ?D)
-> SatisfiedSlipRing(?sr) ^ hasDimensionOuterDiameter_D_unit_mm(?sr, ?D) ^ hasDimensionMaxDiameter_D_unit_mm(?sr, ?D) ^ hasMax1_1(?sr, ?D)


SlipRingSatisfy1Rule:
_1000MEthernet(?sr) ^ CurrentRequirements(?r) ^
has100MBASET(?sr, ?m100) ^ has1000MBASET(?sr, ?m1000) ^
needOutputCommunicationReq(?r, ?noc) ^ needContinuousRotationReq(?r, ?ncr) ^
swrlb:equal(?ncr, true) ^ swrlb:equal(?noc, true) ^ swrlb:add(?nc, ?m100, ?m1000) ^ swrlb:greaterThanOrEqual(?nc, 1)
-> SatisfiedSlipRing(?sr)

SlipRingSatisfy2Rule:
_1000MEthernet(?sr) ^ CurrentRequirements(?r) ^
needOutputCommunicationReq(?r, ?noc) ^ needContinuousRotationReq(?r, ?ncr) ^
swrlb:equal(?ncr, true) ^ swrlb:equal(?noc, false)
-> SatisfiedSlipRing(?sr)


SlipRingEvaluate1Rule:
SatisfiedSlipRing(?sr) ^ _1000MEthernet(?sr) ^
hasDimensionOuterDiameter_D_unit_mm(?sr, ?D_sr) ^ hasDimensionMaxDiameter_D_unit_mm(?sr, ?D_max_sr) ^
swrlb:greaterThan(?D_sr, ?D_max_sr)
-> has1(?sr, ?D_sr)

SlipRingEvaluate2Rule:
SatisfiedSlipRing(?sr) ^ _1000MEthernet(?sr) ^
hasDimensionOuterDiameter_D_unit_mm(?sr, ?D_sr) ^ hasDimensionMaxDiameter_D_unit_mm(?sr, ?D_max_sr) ^
swrlb:lessThanOrEqual(?D_sr, ?D_max_sr)
-> has1(?sr, ?D_max_sr)

SlipRingEvaluate3Rule:
SatisfiedSlipRing(?sr) ^ _1000MEthernet(?sr) ^
has1(?sr, ?D_sr) ^ hasDimensionInnerDiameter_d_unit_mm(?sr, ?d_sr) ^
swrlb:greaterThan(?D_sr, ?d_sr)
-> hasMax1_1(?sr, ?D_sr)

SlipRingEvaluate4Rule:
SatisfiedSlipRing(?sr) ^ _1000MEthernet(?sr) ^
has1(?sr, ?D_sr) ^ hasDimensionInnerDiameter_d_unit_mm(?sr, ?d_sr) ^
swrlb:lessThanOrEqual(?D_sr, ?d_sr)
-> hasMax1_1(?sr, ?d_sr)




TorqueSensorStrainGaugeBased_HollowShaftSatisfyRule:
StrainGaugeBased_HollowShaft(?ts) ^ CurrentRequirements(?r) ^
needTorqueSensorStrainGaugeBasedReq(?r, ?ntssgb) ^
swrlb:equal(?ntssgb, true)
-> SatisfiedTorqueSensor(?ts)


TorqueSensorNoneShaftSatisfyRule:
NoTorqueSensor(?ts) ^ CurrentRequirements(?r) ^
needTorqueSensorStrainGaugeBasedReq(?r, ?ntssgb) ^
swrlb:equal(?ntssgb, false)
-> SatisfiedTorqueSensor(?ts)



SlipRingTorqueSensorMatchSatisfy1Rule:
SlipRingTorqueSensorMatch(?srtsm) ^ hasSlipRing(?srtsm, ?sr) ^ _1000MEthernet(?sr) ^ hasTorqueSensor(?srtsm, ?ts) ^ CurrentRequirements(?r) ^
hasSignal_2A(?sr, ?s2a) ^ has10A(?sr, ?s10a) ^ has20A(?sr, ?s20a) ^ hasSignal_2A(?ts, ?s2a_ts) ^
hasCurrentOutputReqMax_I_unit_A(?r, ?co) ^ hasNumberOfOutputEmergencyStopReq(?r, ?nooes) ^ hasNumberOfOtherCablesReq(?r, ?nooc) ^
swrlb:subtract(?restSignal1, ?s2a, ?nooc) ^ swrlb:subtract(?restSignal2, ?restSignal1, ?nooes) ^ swrlb:subtract(?restSignal3, ?restSignal2, ?s2a_ts) ^ swrlb:multiply(?restSignalInA, ?restSignal3, 2.0) ^ swrlb:multiply(?s10aInA, ?s10a, 10.0) ^ swrlb:multiply(?s20aInA, ?s20a, 20.0) ^ swrlb:add(?sumInA1, ?restSignalInA, ?s10aInA) ^ swrlb:add(?sumInA2, ?sumInA1, ?s20aInA) ^ swrlb:divide(?sumHalf, ?sumInA2, 2.0) ^ swrlb:greaterThanOrEqual(?sumHalf, ?co)
-> HalfSatisfiedSlipRingTorqueSensorMatch(?srtsm)


SlipRingTorqueSensorMatchSatisfy2Rule:
SlipRingTorqueSensorMatch(?srtsm) ^ hasSlipRing(?srtsm, ?sr) ^ NoSlipRing(?sr)
-> SatisfiedSlipRingTorqueSensorMatch(?srtsm)


SlipRingTorqueSensorMatchUnsatisfied1Rule:
HalfSatisfiedSlipRingTorqueSensorMatch(?srtsm1) ^ hasSlipRing(?srtsm1, ?sr1) ^ HalfSatisfiedSlipRingTorqueSensorMatch(?srtsm2) ^ hasSlipRing(?srtsm2, ?sr2) ^
hasTorqueSensor(?srtsm1, ?ts) ^ hasTorqueSensor(?srtsm2, ?ts) ^
hasDimensionLength_L_unit_mm(?sr1, ?L_sr1) ^ hasMax1_1(?sr1, ?D_sr1) ^ hasDimensionLength_L_unit_mm(?sr2, ?L_sr2) ^ hasMax1_1(?sr2, ?D_sr2) ^
swrlb:greaterThan(?L_sr1, ?L_sr2) ^ swrlb:lessThan(?L_sr2, ?L_sr1) ^ swrlb:greaterThan(?D_sr1, ?D_sr2) ^ swrlb:lessThan(?D_sr2, ?D_sr1)
-> UnsatisfiedSlipRingTorqueSensorMatch(?srtsm1)

SlipRingTorqueSensorMatchUnsatisfied2Rule:
HalfSatisfiedSlipRingTorqueSensorMatch(?srtsm1) ^ hasSlipRing(?srtsm1, ?sr1) ^ HalfSatisfiedSlipRingTorqueSensorMatch(?srtsm2) ^ hasSlipRing(?srtsm2, ?sr2) ^
hasTorqueSensor(?srtsm1, ?ts) ^ hasTorqueSensor(?srtsm2, ?ts) ^
hasDimensionLength_L_unit_mm(?sr1, ?L_sr1) ^ hasMax1_1(?sr1, ?D_sr1) ^ hasDimensionLength_L_unit_mm(?sr2, ?L_sr2) ^ hasMax1_1(?sr2, ?D_sr2) ^
swrlb:greaterThan(?L_sr1, ?L_sr2) ^ swrlb:lessThan(?L_sr2, ?L_sr1) ^ swrlb:equal(?D_sr2, ?D_sr1)
-> UnsatisfiedSlipRingTorqueSensorMatch(?srtsm1)

SlipRingTorqueSensorMatchUnsatisfied3Rule:
HalfSatisfiedSlipRingTorqueSensorMatch(?srtsm1) ^ hasSlipRing(?srtsm1, ?sr1) ^ HalfSatisfiedSlipRingTorqueSensorMatch(?srtsm2) ^ hasSlipRing(?srtsm2, ?sr2) ^
hasTorqueSensor(?srtsm1, ?ts) ^ hasTorqueSensor(?srtsm2, ?ts) ^
hasDimensionLength_L_unit_mm(?sr1, ?L_sr1) ^ hasMax1_1(?sr1, ?D_sr1) ^ hasDimensionLength_L_unit_mm(?sr2, ?L_sr2) ^ hasMax1_1(?sr2, ?D_sr2) ^
swrlb:equal(?L_sr2, ?L_sr1) ^ swrlb:greaterThan(?D_sr1, ?D_sr2) ^ swrlb:lessThan(?D_sr2, ?D_sr1)
-> UnsatisfiedSlipRingTorqueSensorMatch(?srtsm1)


SlipRingTorqueSensorMatchPossibleSatisfiedRule:
HalfSatisfiedSlipRingTorqueSensorMatch(?srtsm1) ^ hasSlipRing(?srtsm1, ?sr1) ^ HalfSatisfiedSlipRingTorqueSensorMatch(?srtsm2) ^ hasSlipRing(?srtsm2, ?sr2) ^
hasTorqueSensor(?srtsm1, ?ts) ^ hasTorqueSensor(?srtsm2, ?ts) ^
hasDimensionLength_L_unit_mm(?sr1, ?L_sr1) ^ hasMax1_1(?sr1, ?D_sr1) ^ hasDimensionLength_L_unit_mm(?sr2, ?L_sr2) ^ hasMax1_1(?sr2, ?D_sr2) ^
swrlb:equal(?L_sr2, ?L_sr1) ^ swrlb:equal(?D_sr2, ?D_sr1)
-> PossibleSatisfiedSlipRingTorqueSensorMatch(?srtsm1) ^ PossibleSatisfiedSlipRingTorqueSensorMatch(?srtsm2)



GearBoxConsiderSlipRingSatisfyRule:
SatisfiedGearBoxInMotorGearBoxMatch(?g) ^ SatisfiedSlipRingTorqueSensorMatch(?srtsm) ^ hasSlipRing(?srtsm, ?sr) ^
hasDimensionInnerDiameterFlexSpline_K_unit_mm(?g, ?d_g) ^ hasDimensionOuterDiameter_D_unit_mm(?sr, ?D_sr) ^
swrlb:greaterThanOrEqual(?d_g, ?D_sr)
-> SatisfiedGearBoxInMotorGearBoxMatchConsiderSlipRing(?g)




DiameterInputDriveBearingMatchLinearSatisfyRule:
DiameterInputDriveBearingMatchLinear(?didbm) ^ hasDiameterInput(?didbm, ?di) ^ hasMotor(?di, ?m) ^ hasDriveBearing(?didbm, ?db) ^
hasDimensionInnerDiameter_d_unit_mm(?m, ?d_m) ^ hasDimensionInnerDiameter_d_unit_mm(?db, ?d_ib) ^
swrlb:lessThanOrEqual(?d_ib, ?d_m)
-> HalfSatisfiedDiameterInputDriveBearingMatchLinear(?didbm)


DiameterInputDriveBearingMatchLinearUnsatisfiedRule:
HalfSatisfiedDiameterInputDriveBearingMatchLinear(?didbm1) ^ hasDiameterInput(?didbm1, ?di) ^ hasDriveBearing(?didbm1, ?db1) ^
HalfSatisfiedDiameterInputDriveBearingMatchLinear(?didbm2) ^ hasDiameterInput(?didbm2, ?di) ^ hasDriveBearing(?didbm2, ?db2) ^
hasDimensionInnerDiameter_d_unit_mm(?db1, ?d_ib1) ^ hasDimensionInnerDiameter_d_unit_mm(?db2, ?d_ib2) ^
swrlb:greaterThan(?d_ib1, ?d_ib2) ^ swrlb:lessThan(?d_ib2, ?d_ib1)
-> UnsatisfiedDiameterInputDriveBearingMatchLinear(?didbm2)

DiameterInputDriveBearingMatchLinearPossibleSatisfiedRule:
HalfSatisfiedDiameterInputDriveBearingMatchLinear(?didbm1) ^ hasDiameterInput(?didbm1, ?di) ^ hasDriveBearing(?didbm1, ?db1) ^
HalfSatisfiedDiameterInputDriveBearingMatchLinear(?didbm2) ^ hasDiameterInput(?didbm2, ?di) ^ hasDriveBearing(?didbm2, ?db2) ^
hasDimensionInnerDiameter_d_unit_mm(?db1, ?d_ib1) ^ hasDimensionInnerDiameter_d_unit_mm(?db2, ?d_ib2) ^
swrlb:equal(?d_ib2, ?d_ib1)
-> PossibleSatisfiedDiameterInputDriveBearingMatchLinear(?didbm1) ^ PossibleSatisfiedDiameterInputDriveBearingMatchLinear(?didbm2)



DiameterInputDriveBearingMatchClinchedSatisfyRule:
DiameterInputDriveBearingMatchClinched(?didbm) ^ hasDiameterInput(?didbm, ?di) ^ hasMotor(?di, ?m) ^ hasDriveBearing(?didbm, ?db) ^ Constants(?con) ^
hasDimensionInnerDiameter_d_unit_mm(?m, ?d_m) ^ hasDimensionOuterDiameter_D_unit_mm(?db, ?D_ib) ^ thicknessMotorShaft(?con, ?tms_con) ^
swrlb:multiply(?tms_con2, 2.0, ?tms_con) ^ swrlb:subtract(?temp, ?d_m, ?tms_con2) ^
swrlb:lessThanOrEqual(?D_ib, ?temp)
-> HalfSatisfiedDiameterInputDriveBearingMatchClinched(?didbm)


DiameterInputDriveBearingMatchClinchedUnsatisfiedRule:
HalfSatisfiedDiameterInputDriveBearingMatchClinched(?didbm1) ^ hasDiameterInput(?didbm1, ?di) ^ hasDriveBearing(?didbm1, ?db1) ^
HalfSatisfiedDiameterInputDriveBearingMatchClinched(?didbm2) ^ hasDiameterInput(?didbm2, ?di) ^ hasDriveBearing(?didbm2, ?db2) ^
hasDimensionOuterDiameter_D_unit_mm(?db1, ?D_ib1) ^ hasDimensionOuterDiameter_D_unit_mm(?db2, ?D_ib2) ^
swrlb:greaterThan(?D_ib1, ?D_ib2) ^ swrlb:lessThan(?D_ib2, ?D_ib1)
-> UnsatisfiedDiameterInputDriveBearingMatchClinched(?didbm2)

DiameterInputDriveBearingMatchClinchedPossibleSatisfiedRule:
HalfSatisfiedDiameterInputDriveBearingMatchClinched(?didbm1) ^ hasDiameterInput(?didbm1, ?di) ^ hasDriveBearing(?didbm1, ?db1) ^
HalfSatisfiedDiameterInputDriveBearingMatchClinched(?didbm2) ^ hasDiameterInput(?didbm2, ?di) ^ hasDriveBearing(?didbm2, ?db2) ^
hasDimensionOuterDiameter_D_unit_mm(?db1, ?D_ib1) ^ hasDimensionOuterDiameter_D_unit_mm(?db2, ?D_ib2) ^
swrlb:equal(?D_ib2, ?D_ib1)
-> PossibleSatisfiedDiameterInputDriveBearingMatchClinched(?didbm1) ^ PossibleSatisfiedDiameterInputDriveBearingMatchClinched(?didbm2)




IncrementalEncoderSatisfy1Rule:
NoIE(?ie) ^ CurrentRequirements(?r) ^ needIncrementalEncoderReq(?r, ?ier) ^ swrlb:equal(?ier, false) ^ needMotorControllerReq(?r, ?nmc) ^ swrlb:equal(?nmc, false) ^
hasDimensionLengthIncrementalEncoderConnector_L_unit_mm(?ie, ?L_iec) 
-> SatisfiedIncrementalEncoder(?ie) ^ has1(?ie, ?L_iec)

IncrementalEncoderSatisfy2Rule:
NoIE(?ie) ^ CurrentRequirements(?r) ^ needIncrementalEncoderReq(?r, ?ier) ^ swrlb:equal(?ier, false) ^ needMotorControllerReq(?r, ?nmc) ^ swrlb:equal(?nmc, true) ^
hasDimensionLengthIncrementalEncoderConnector_L_unit_mm(?ie, ?L_iec) 
-> SatisfiedIncrementalEncoder(?ie) ^ has1(?ie, 0.0)

IncrementalEncoderSatisfy3Rule:
WithIE(?ie) ^ CurrentRequirements(?r) ^ needIncrementalEncoderReq(?r, ?ier) ^ swrlb:equal(?ier, true) ^ needMotorControllerReq(?r, ?nmc) ^ swrlb:equal(?nmc, false) ^
hasDimensionLengthIncrementalEncoderConnector_L_unit_mm(?ie, ?L_iec) 
-> SatisfiedIncrementalEncoder(?ie) ^ has1(?ie, ?L_iec)

IncrementalEncoderSatisfy4Rule:
WithIE(?ie) ^ CurrentRequirements(?r) ^ needIncrementalEncoderReq(?r, ?ier) ^ swrlb:equal(?ier, true) ^ needMotorControllerReq(?r, ?nmc) ^ swrlb:equal(?nmc, true) ^
hasDimensionLengthIncrementalEncoderConnector_L_unit_mm(?ie, ?L_iec) 
-> SatisfiedIncrementalEncoder(?ie) ^ has1(?ie, 0.0)




LengthInputLinearCalcRule:
LengthInputLinear(?lil) ^ hasDiameterInputDriveBearingMatch(?lil, ?didbm) ^ hasIncrementalEncoder(?lil, ?ie) ^ hasGearBox(?lil, ?g) ^ hasDriveBearing(?didbm, ?db) ^ hasDiameterInput(?didbm, ?di) ^ hasMotor(?di, ?m) ^ Constants(?con) ^
hasDimensionLength_L_unit_mm(?ie, ?L_ie) ^ hasDimensionLength_L_unit_mm(?m, ?L_m) ^ hasDimensionLength_L_unit_mm(?db, ?L_ib) ^ lengthFloatBearingOffset(?con, ?fbo_con) ^ lengthMotorCover(?con, ?mc_con) ^ lengthMotorWiring(?con, ?mw_con) ^ hasDimensionSafety_c_unit_mm(?g, ?c_g) ^
swrlb:multiply(?L_fixedB, ?L_ib, 2.0) ^ swrlb:add(?L_temp1, ?L_m, ?L_fixedB) ^ swrlb:add(?L_1, ?L_temp1, ?c_g) ^ swrlb:add(?L_floatB, ?L_ib, ?fbo_con) ^ swrlb:add(?L_max1, ?L_floatB, ?L_ie) ^ swrlb:add(?L_max2, ?mc_con, ?mw_con)
-> has1(?lil, ?L_1) ^ hasMax1_1(?lil, ?L_max1) ^ hasMax1_2(?lil, ?L_max2) ^ SatisfiedLengthInputLinear(?lil)


LengthInputLinearEvaluate1Rule:
SatisfiedLengthInputLinear(?lil) ^ has1(?lil, ?L_1) ^ hasMax1_1(?lil, ?L_max1) ^ hasMax1_2(?lil, ?L_max2) ^ swrlb:greaterThan(?L_max1, ?L_max2) ^ swrlb:add(?L_ges, ?L_1, ?L_max1) -> hasDimensionLength_L_unit_mm(?lil, ?L_ges)

LengthInputLinearEvaluate2Rule:
SatisfiedLengthInputLinear(?lil) ^ has1(?lil, ?L_1) ^ hasMax1_1(?lil, ?L_max1) ^ hasMax1_2(?lil, ?L_max2) ^ swrlb:lessThanOrEqual(?L_max1, ?L_max2) ^ swrlb:add(?L_ges, ?L_1, ?L_max2) -> hasDimensionLength_L_unit_mm(?lil, ?L_ges)



LengthInputClinchedCalcRule:
LengthInputClinched(?lic) ^ hasDiameterInputDriveBearingMatch(?lic, ?didbm) ^ hasIncrementalEncoder(?lic, ?ie) ^ hasGearBox(?lic, ?g) ^ hasDriveBearing(?didbm, ?db) ^ hasDiameterInput(?didbm, ?di) ^ hasMotor(?di, ?m) ^ Constants(?con) ^
hasDimensionLength_L_unit_mm(?ie, ?L_ie) ^ hasDimensionLength_L_unit_mm(?m, ?L_m) ^ hasDimensionLength_L_unit_mm(?db, ?L_ib) ^ lengthFloatBearingOffset(?con, ?fbo_con) ^ hasDimensionSafety_c_unit_mm(?g, ?c_g) ^ lengthMotorCover(?con, ?mc_con) ^ lengthMotorWiring(?con, ?mw_con) ^ lengthMotorStopBracket(?con, ?msb_con) ^ lengthRotationGap(?con, ?rg_con) ^
swrlb:add(?temp1, ?mc_con, ?mw_con) ^ swrlb:add(?temp2, ?temp1, ?L_m) ^ swrlb:add(?temp3, ?temp2, ?msb_con) ^ swrlb:add(?L_max1_1, ?temp3, ?c_g) ^
swrlb:add(?L_floatB, ?L_ib, ?fbo_con) ^ swrlb:multiply(?L_fixedB, ?L_ib, 2.0) ^ swrlb:add(?L_1, ?L_floatB, ?L_fixedB) ^
swrlb:add(?L_max2_2, ?mc_con, ?rg_con)
-> has1(?lic, ?L_1) ^ hasMax1_1(?lic, ?L_max1_1) ^ hasMax2_1(?lic, ?L_ie) ^ hasMax2_2(?lic, ?L_max2_2) ^ SatisfiedLengthInputClinched(?lic)


LengthInputClinchedEvaluate1Rule:
SatisfiedLengthInputClinched(?lic) ^
hasMax2_1(?lic, ?L_max2_1) ^ hasMax2_2(?lic, ?L_max2_2) ^ has1(?lic, ?L_1) ^
swrlb:greaterThan(?L_max2_1, ?L_max2_2) ^ swrlb:add(?L_max1_2, ?L_1, ?L_max2_1)
-> hasMax1_2(?lic, ?L_max1_2)

LengthInputClinchedEvaluate2Rule:
SatisfiedLengthInputClinched(?lic) ^
hasMax2_1(?lic, ?L_max2_1) ^ hasMax2_2(?lic, ?L_max2_2) ^ has1(?lic, ?L_1) ^
swrlb:lessThanOrEqual(?L_max2_1, ?L_max2_2) ^ swrlb:add(?L_max1_2, ?L_1, ?L_max2_2)
-> hasMax1_2(?lic, ?L_max1_2)

LengthInputClinchedEvaluate3Rule:
SatisfiedLengthInputClinched(?lic) ^
hasMax1_1(?lic, ?L_max1_1) ^ hasMax1_2(?lic, ?L_max1_2)
swrlb:greaterThan(?L_max1_1, ?L_max1_2)
-> hasDimensionLength_L_unit_mm(?lic, ?L_max1_1)

LengthInputClinchedEvaluate4Rule:
SatisfiedLengthInputClinched(?lic) ^
hasMax1_1(?lic, ?L_max1_1) ^ hasMax1_2(?lic, ?L_max1_2)
swrlb:lessThanOrEqual(?L_max1_1, ?L_max1_2)
-> hasDimensionLength_L_unit_mm(?lic, ?L_max1_2)



AbsolutEncoderWithSatisfyRule:
AbsolutEncoderMBAD01_05(?ab) ^ CurrentRequirements(?r) ^ needAbsoluteEncoderReq(?r, ?ab_r) ^ swrlb:equal(?ab_r, true) -> SatisfiedAbsoluteEncoder(?ab)

AbsolutEncoderNoneSatisfyRule:
NoAbsolutEncoder(?ab) ^ CurrentRequirements(?r) ^ needAbsoluteEncoderReq(?r, ?ab_r) ^ swrlb:equal(?ab_r, false) -> SatisfiedAbsoluteEncoder(?ab)


SensorPCBWithSatisfyRule:
WithSensorPCB(?spcb) ^ CurrentRequirements(?r) ^ needSensorControlBoardReq(?r, ?spcm) ^ swrlb:equal(?spcm, true) -> SatisfiedSensorPCB(?spcb)

SensorPCBNoneSatisfyRule:
NoSensorPCB(?spcb) ^ CurrentRequirements(?r) ^ needSensorControlBoardReq(?r, ?spcm) ^ swrlb:equal(?spcm, false) -> SatisfiedSensorPCB(?spcb)



AbsolutEncoderSensorPCBMatchSatisfy1Rule:
AbsoluteEncoderSensorPCBMatch(?abspcb) ^ hasAbsoluteEncoder(?abspcb, ?ab) ^ hasSensorPCB(?abspcb, ?spcb) ^ Constants(?con) ^
hasDimensionLength_L_unit_mm(?ab, ?L_ab) ^ hasDimensionLength_L_unit_mm(?spcb, ?L_spcb) ^ lengthAbsoluteEncoderWithSensorPCB(?con, ?L_aewspcb) ^
swrlb:add(?L_temp, ?L_ab, ?L_spcb) ^
swrlb:greaterThan(?L_temp, ?L_aewspcb) -> hasDimensionLength_L_unit_mm(?abspcb, ?L_aewspcb) ^ SatisfiedAbsoluteEncoderSensorPCBMatch(?abspcb)

AbsolutEncoderSensorPCBMatchSatisfy2Rule:
AbsoluteEncoderSensorPCBMatch(?abspcb) ^ hasAbsoluteEncoder(?abspcb, ?ab) ^ hasSensorPCB(?abspcb, ?spcb) ^ Constants(?con) ^
hasDimensionLength_L_unit_mm(?ab, ?L_ab) ^ hasDimensionLength_L_unit_mm(?spcb, ?L_spcb) ^ lengthAbsoluteEncoderWithSensorPCB(?con, ?L_aewspcb) ^
swrlb:add(?L_temp, ?L_ab, ?L_spcb) ^
swrlb:lessThanOrEqual(?L_temp, ?L_aewspcb) -> hasDimensionLength_L_unit_mm(?abspcb, ?L_temp) ^ SatisfiedAbsoluteEncoderSensorPCBMatch(?abspcb)





GearBoxAbsolutEncoderSensorPCBMatchCalc1Rule:
GearBoxAbsolutEncoderSensorPCBMatch(?gaespcb) ^ hasGearBox(?gaespcb, ?g) ^ hasAbsoluteEncoderSensorPCBMatch(?gaespcb, ?abspcb) ^ hasAbsoluteEncoder(?abspcb, ?ab) ^ AbsolutEncoderMBAD01_05(?ab) ^
hasDimensionDiameterPitchCircleFlexSpline_T_unit_mm(?g, ?T_g) ^ hasDimensionScrewInnerDiameterFlexSplineM_d_unit_mm(?g, ?D_s) ^ hasDimensionInnerDiameter_d_unit_mm(?ab, ?d_ab) ^
swrlb:subtract(?D_temp, ?T_g, ?D_s) ^
swrlb:greaterThanOrEqual(?d_ab, ?D_temp)
-> HalfSatisfiedGearBoxAbsolutEncoderSensorPCBMatch(?gaespcb)

GearBoxAbsolutEncoderSensorPCBMatchCalc2Rule:
GearBoxAbsolutEncoderSensorPCBMatch(?gaespcb) ^ hasAbsoluteEncoder(?abspcb, ?ab) ^ NoAbsolutEncoder(?ab)
-> SatisfiedGearBoxAbsolutEncoderSensorPCBMatch(?gaespcb)


GearBoxAbsolutEncoderSensorPCBMatchUnsatisfiedRule:
HalfSatisfiedGearBoxAbsolutEncoderSensorPCBMatch(?gaespcb1) ^ hasGearBox(?gaespcb1, ?g) ^ hasAbsoluteEncoderSensorPCBMatch(?gaespcb1, ?abspcb1) ^ hasAbsoluteEncoder(?abspcb1, ?ab1) ^ hasSensorPCB(?abspcb1, ?spcb) ^
HalfSatisfiedGearBoxAbsolutEncoderSensorPCBMatch(?gaespcb2) ^ hasGearBox(?gaespcb2, ?g) ^ hasAbsoluteEncoderSensorPCBMatch(?gaespcb2, ?abspcb2) ^ hasAbsoluteEncoder(?abspcb2, ?ab2) ^ hasSensorPCB(?abspcb2, ?spcb) ^
hasDimensionOuterDiameter_D_unit_mm(?ab1, ?D_ab1) ^ hasDimensionOuterDiameter_D_unit_mm(?ab2, ?D_ab2) ^
swrlb:greaterThan(?D_ab1, ?D_ab2) ^ swrlb:lessThan(?D_ab2, ?D_ab1)
-> UnsatisfiedGearBoxAbsolutEncoderSensorPCBMatch(?gaespcb1)

GearBoxAbsolutEncoderSensorPCBMatchPossibleSatisfiedRule:
HalfSatisfiedGearBoxAbsolutEncoderSensorPCBMatch(?gaespcb1) ^ hasGearBox(?gaespcb1, ?g) ^ hasAbsoluteEncoderSensorPCBMatch(?gaespcb1, ?abspcb1) ^ hasAbsoluteEncoder(?abspcb1, ?ab1) ^ hasSensorPCB(?abspcb1, ?spcb) ^
HalfSatisfiedGearBoxAbsolutEncoderSensorPCBMatch(?gaespcb2) ^ hasGearBox(?gaespcb2, ?g) ^ hasAbsoluteEncoderSensorPCBMatch(?gaespcb2, ?abspcb2) ^ hasAbsoluteEncoder(?abspcb2, ?ab2) ^ hasSensorPCB(?abspcb2, ?spcb) ^
hasDimensionOuterDiameter_D_unit_mm(?ab1, ?D_ab1) ^ hasDimensionOuterDiameter_D_unit_mm(?ab2, ?D_ab2) ^
swrlb:equal(?D_ab2, ?D_ab1)
-> PossibleSatisfiedGearBoxAbsolutEncoderSensorPCBMatch(?gaespcb1) ^ PossibleSatisfiedGearBoxAbsolutEncoderSensorPCBMatch(?gaespcb2)




DiameterOutputCircularScrewMatchSatisfyRule:
DiameterOutputCircularScrewMatch(?gs) ^ hasDiameterOutput(?gs, ?do) ^ hasScrew(?gs, ?s) ^ hasGearBoxAbsolutEncoderSensorPCBMatch(?do, ?gaespcb) ^ hasGearBox(?gaespcb, ?g) ^ hasDimensionScrewInnerDiameterCircularSplineM_d_unit_mm(?g, ?d_s1) ^ hasDimensionScrewInnerDiameterM_d_unit_mm(?s, ?d_s2) ^ swrlb:equal(?d_s1, ?d_s2) -> SatisfiedDiameterOutputCircularScrewMatch(?gs)


OutputBearingScrewMatchSatisfyRule:
OutputBearingScrewMatch(?obs) ^ hasOutputBearing(?obs, ?ob) ^ hasScrew(?obs, ?s) ^ hasDimensionScrewInnerDiameterM_d_unit_mm(?ob, ?d_s1) ^ hasDimensionScrewInnerDiameterM_d_unit_mm(?s, ?d_s2) ^ swrlb:equal(?d_s1, ?d_s2) -> SatisfiedOutputBearingScrewMatch(?obs)





LengthOutputLinearFlexScrewMatchCalc1Rule:
LengthOutputLinearFlexScrewMatch(?lol) ^ hasDiameterOutputBearing(?lol, ?dob) ^ hasDiameterOutputCircularScrewMatch(?dob, ?gs) ^ hasOutputBearingScrewMatch(?dob, ?obs) ^ hasSlipRingTorqueSensorMatch(?dob, ?srtsm) ^ hasTorsionalWave(?dob, ?tw) ^ hasTorqueSensor(?srtsm, ?ts) ^ hasDiameterOutput(?gs, ?do) ^ hasGearBoxAbsolutEncoderSensorPCBMatch(?do, ?gaespcb) ^ hasGearBox(?gaespcb, ?g) ^ hasAbsoluteEncoderSensorPCBMatch(?gaespcb, ?abspcb) ^ hasScrew(?lol, ?s_lol) ^ hasOutputBearing(?obs, ?ob) ^ hasScrew(?obs, ?s_ob) ^ Constants(?con) ^
hasLengthInput(?lol, ?li) ^ hasGearBox(?li, ?g) ^ hasDiameterInputDriveBearingMatch(?li, ?didbm) ^ hasDiameterInput(?didbm, ?di) ^ hasMotor(?di, ?m) ^ SatisfiedMotorGearBoxMatchForSACunit(?ma) ^ hasGearBox(?ma, ?g) ^ hasMotor(?ma, ?m) ^
hasDimensionScrewInnerDiameterFlexSplineM_d_unit_mm(?g, ?d_s1) ^ hasDimensionScrewInnerDiameterM_d_unit_mm(?s_lol, ?d_s2) ^ swrlb:equal(?d_s1, ?d_s2) ^ hasDimensionLengthScrewHead_k_unit_mm(?s_lol, ?L_s_lol) ^ hasDimensionScrewInnerDiameterM_d_unit_mm(?s_lol, ?d_s_lol) ^ hasDimensionLength_L_unit_mm(?ob, ?L_ob) ^ hasDimensionLengthScrewHead_k_unit_mm(?s_ob, ?L_s_ob) ^ hasDimensionLength_L_unit_mm(?abspcb, ?L_loie) ^  hasDimensionLengthFaktor(?tw, ?Lf_tw) ^ hasDimensionLength_L_unit_mm(?ts, ?L_ts) ^ lengthCentering(?con, ?c_con) ^ lengthNotch(?con, ?n_con) ^ lengthMeasurementWidth(?con, ?mw_con) ^
swrlb:multiply(?L_2kerbe, 2.0, ?n_con) ^ swrlb:add(?L_torWelleTemp1, ?mw_con, ?L_2kerbe) ^ swrlb:multiply(?L_torWelleTemp2, ?d_s_lol, 1.5) ^ swrlb:multiply(?L_torWelleTemp3, 2.0, ?L_torWelleTemp2) ^ swrlb:add(?L_torWelleTemp4, ?L_torWelleTemp1, ?L_torWelleTemp3) ^ swrlb:multiply(?L_torWelle, ?Lf_tw, ?L_torWelleTemp4) ^
swrlb:add(?L_max1_1, ?L_torWelle, ?L_s_lol) ^ swrlb:multiply(?L_abLagerTemp1, 1.5, ?L_ob) ^ swrlb:add(?L_abLager, ?L_abLagerTemp1, ?c_con) ^ swrlb:add(?L_max1_2, ?L_loie, ?L_abLager) ^ swrlb:add(?L_max2_1, ?L_ts, ?L_s_lol) ^ swrlb:multiply(?L_max2_2, 2.0, ?L_s_ob)
-> hasMax1_1(?lol, ?L_max1_1) ^ hasMax1_2(?lol, ?L_max1_2) ^ hasMax2_1(?lol, ?L_max2_1) ^ hasMax2_2(?lol, ?L_max2_2)


LengthOutputLinearFlexScrewMatchCalc2Rule:
LengthOutputLinearFlexScrewMatch(?lol) ^ hasLengthInput(?lol, ?li) ^ hasDiameterOutputBearing(?lol, ?dob) ^ hasDiameterOutputCircularScrewMatch(?dob, ?gs) ^ hasSlipRingTorqueSensorMatch(?dob, ?srtsm) ^ hasSlipRing(?srtsm, ?sr) ^ hasDiameterOutput(?gs, ?do) ^ hasGearBoxAbsolutEncoderSensorPCBMatch(?do, ?gaespcb) ^ hasGearBox(?gaespcb, ?g) ^ Constants(?con) ^
hasDimensionInnerDiameterWaveGenerator_unit_mm(?g, ?d_wg_g) ^ hasMax1_1(?sr, ?d_sr) ^ diameterSafetySlipRingInGearBox(?c, ?c_d) ^
swrlb:add(?d_max, ?d_sr, ?c_d) ^ swrlb:greaterThanOrEqual(?d_wg_g, ?d_max) ^
hasDimensionLengthInner_L_unit_mm(?sr, ?l_sr) ^ hasDimensionLength_L_unit_mm(?sr, ?L_sr) ^ hasDimensionLength_L_unit_mm(?li, ?L_li) ^ hasDimensionLength_L_unit_mm(?g, ?L_g) ^
swrlb:add(?temp1, ?l_sr, ?L_sr) ^ swrlb:subtract(?temp2, ?temp1, ?L_li) ^ swrlb:subtract(?L_max3_2, ?temp2, ?L_g)
-> hasMax3_2(?lol, ?L_max3_2)


LengthOutputLinearFlexScrewMatchCalc3Rule:
LengthOutputLinearFlexScrewMatch(?lol) ^ hasDiameterOutputBearing(?lol, ?dob) ^ hasDiameterOutputCircularScrewMatch(?dob, ?gs) ^ hasSlipRingTorqueSensorMatch(?dob, ?srtsm) ^ hasSlipRing(?srtsm, ?sr) ^ hasDiameterOutput(?gs, ?do) ^ hasGearBoxAbsolutEncoderSensorPCBMatch(?do, ?gaespcb) ^ hasGearBox(?gaespcb, ?g) ^ Constants(?con) ^
hasDimensionInnerDiameterWaveGenerator_unit_mm(?g, ?d_wg_g) ^ hasMax1_1(?sr, ?d_sr) ^ diameterSafetySlipRingInGearBox(?c, ?c_d) ^
swrlb:add(?d_max, ?d_sr, ?c_d) ^ swrlb:lessThan(?d_wg_g, ?d_max) ^
hasDimensionLength_L_unit_mm(?sr, ?L_sr) ^ hasDimensionLength_L_unit_mm(?g, ?L_g) ^ hasDimensionLengthWaveGenerator_F_unit_mm(?g, ?L_wg_g) ^
swrlb:add(?temp1, ?L_sr, ?L_g) ^ swrlb:subtract(?L_max3_2, ?temp1, ?L_wg_g)
-> hasMax3_2(?lol, ?L_max3_2)



LengthOutputLinearFlexScrewMatchEvaluate1Rule:
LengthOutputLinearFlexScrewMatch(?lol) ^
hasMax1_1(?lol, ?L_max1_1) ^ hasMax1_2(?lol, ?L_max1_2) ^
swrlb:greaterThan(?L_max1_1, ?L_max1_2)
-> has1(?lol, ?L_max1_1)

LengthOutputLinearFlexScrewMatchEvaluate2Rule:
LengthOutputLinearFlexScrewMatch(?lol) ^
hasMax1_1(?lol, ?L_max1_1) ^
hasMax1_2(?lol, ?L_max1_2) ^
swrlb:lessThanOrEqual(?L_max1_1, ?L_max1_2)
-> has1(?lol, ?L_max1_2)

LengthOutputLinearFlexScrewMatchEvaluate3Rule:
LengthOutputLinearFlexScrewMatch(?lol) ^
has1(?lol, ?L_part1) ^ hasMax2_1(?lol, ?L_max2_1) ^ hasMax2_2(?lol, ?L_max2_2) ^
swrlb:greaterThan(?L_max2_1, ?L_max2_2) ^ swrlb:add(?L_ges, ?L_part1, ?L_max2_1)
-> hasMax3_1(?lol, ?L_ges)

LengthOutputLinearFlexScrewMatchEvaluate4Rule:
LengthOutputLinearFlexScrewMatch(?lol) ^
has1(?lol, ?L_part1) ^ hasMax2_1(?lol, ?L_max2_1) ^ hasMax2_2(?lol, ?L_max2_2) ^
swrlb:lessThanOrEqual(?L_max2_1, ?L_max2_2) ^ swrlb:add(?L_ges, ?L_part1, ?L_max2_2)
-> hasMax3_1(?lol, ?L_ges)

LengthOutputLinearFlexScrewMatchEvaluate5Rule:
LengthOutputLinearFlexScrewMatch(?lol) ^
hasMax3_1(?lol, ?L_max3_1) ^ hasMax3_2(?lol, ?L_max3_2) ^
swrlb:greaterThan(?L_max3_1, ?L_max3_2)
-> hasDimensionLength_L_unit_mm(?lol, ?L_max3_1) ^ SatisfiedLengthOutputLinearFlexScrewMatch(?lol)

LengthOutputLinearFlexScrewMatchEvaluate6Rule:
LengthOutputLinearFlexScrewMatch(?lol) ^
hasMax3_1(?lol, ?L_max3_1) ^ hasMax3_2(?lol, ?L_max3_2) ^
swrlb:lessThanOrEqual(?L_max3_1, ?L_max3_2)
-> hasDimensionLength_L_unit_mm(?lol, ?L_max3_2) ^ SatisfiedLengthOutputLinearFlexScrewMatch(?lol)




LengthCalcRule:
Length(?l) ^ hasLengthOutput(?l, ?lo) ^ hasLengthInput(?lo, ?li) ^ hasIncrementalEncoder(?li, ?ie) ^ hasGearBox(?li, ?g) ^ hasDiameterInputDriveBearingMatch(?li, ?didbm) ^ hasDiameterInput(?didbm, ?di) ^ hasMotorController(?di, ?mc) ^
hasDimensionLength_L_unit_mm(?li, ?L_li) ^ hasDimensionLength_L_unit_mm(?lo, ?L_lo) ^ has1(?ie, ?L_iec) ^ hasDimensionLengthElectric_L_uni_mm(?mc, ?L_e)  ^ hasDimensionLength_L_unit_mm(?g, ?L_g) ^
swrlb:add(?L_temp1, ?L_li, ?L_g) ^ swrlb:add(?L_temp2, ?L_temp1, ?L_lo) ^ swrlb:add(?L_temp3, ?L_temp2, ?L_iec) ^ swrlb:add(?L_ges, ?L_temp3, ?L_e)
-> hasDimensionLength_L_unit_mm(?l, ?L_ges)


LengthSatisfyRule:
Length(?l) ^ hasDimensionLength_L_unit_mm(?l, ?L_ges) ^ CurrentRequirements(?r) ^ hasDimensionLengthReqMax_L_unit_mm(?r, ?L_req_max) ^ swrlb:lessThanOrEqual(?L_ges, ?L_req_max) ^ hasDimensionLengthReqMin_L_unit_mm(?r, ?L_req_min) ^ swrlb:greaterThanOrEqual(?L_ges, ?L_req_min) -> SatisfiedLength(?l)






DiameterInputCalcRule:
DiameterInput(?di) ^ hasMotor(?di, ?m) ^ hasMotorController(?di, ?mc) ^ hasDimensionOuterDiameter_D_unit_mm(?m, ?D_m) ^ hasDimensionOuterDiameter_D_unit_mm(?mc, ?D_mc) ^ CurrentRequirements(?r) ^ hasThicknessEngineCaseReq_D_unit_mm(?r, ?tec_r) ^ swrlb:multiply(?dec, ?tec_r, 2.0) ^ swrlb:multiply(?dmc, ?D_mc, 2.0) ^ swrlb:add(?temp, ?dec, ?dmc) ^ swrlb:add(?d_input, ?temp, ?D_m) -> hasDimensionOuterDiameter_D_unit_mm(?di, ?d_input)



DiameterInputSatisfyRule:
DiameterInput(?di) ^ hasDimensionOuterDiameter_D_unit_mm(?di, ?D_input) ^ CurrentRequirements(?r) ^ hasDimensionOuterDiameterReqMax_D_unit_mm(?r, ?D_req_max) ^ swrlb:lessThanOrEqual(?D_input, ?D_req_max) -> SatisfiedDiameterInput(?di)




DiameterOutputCalcRule:
DiameterOutput(?do) ^ hasGearBoxAbsolutEncoderSensorPCBMatch(?do, ?gaespcb) ^ hasGearBox(?gaespcb, ?g) ^ hasAbsoluteEncoderSensorPCBMatch(?gaespcb, ?abspcb) ^ hasAbsoluteEncoder(?abspcb, ?ab) ^ Constants(?con) ^ CurrentRequirements(?r) ^
hasDimensionOuterDiameter_D_unit_mm(?g, ?D_g) ^ diameterWiring(?con, ?D_w) ^ hasDimensionScrewInnerDiameterCircularSplineM_d_unit_mm(?g, ?D_scp) ^ hasDimensionOuterDiameter_D_unit_mm(?ab, ?D_ab) ^  hasThicknessOutputCaseReq_D_unit_mm(?r, ?D_oc) ^ 
swrlb:multiply(?D_w2, ?D_w, 2.0) ^ swrlb:multiply(?D_scp2, ?D_scp, 2.0) ^ swrlb:add(?temp1, ?D_g, ?D_w2) ^ swrlb:add(?D_max1, ?temp1, ?D_scp2) ^ swrlb:multiply(?D_oc2, ?D_oc, 2.0) ^ swrlb:add(?D_max2, ?D_oc2, ?D_ab)
-> hasMax1_1(?do, ?D_max1) ^ hasMax1_2(?do, ?D_max2)



DiameterOutputEvaluate1Rule:
DiameterOutput(?do) ^ hasMax1_1(?do, ?D_max1) ^ hasMax1_2(?do, ?D_max2) ^ swrlb:greaterThan(?D_max1, ?D_max2) -> hasDimensionOuterDiameter_D_unit_mm(?do, ?D_max1)

DiameterOutputEvaluate2Rule:
DiameterOutput(?do) ^ hasMax1_1(?do, ?D_max1) ^ hasMax1_2(?do, ?D_max2) ^ swrlb:lessThanOrEqual(?D_max1, ?D_max2) -> hasDimensionOuterDiameter_D_unit_mm(?do, ?D_max2)



DiameterOutputSatisfyRule:
DiameterOutput(?do) ^ hasDimensionOuterDiameter_D_unit_mm(?do, ?D_out) ^ CurrentRequirements(?r) ^ hasDimensionOuterDiameterReqMax_D_unit_mm(?r, ?D_req_max) ^ swrlb:lessThanOrEqual(?D_out, ?D_req_max) -> SatisfiedDiameterOutput(?do)




DiameterOutputBearingCalcRule:
DiameterOutputBearing(?dob) ^ hasDiameterOutputCircularScrewMatch(?dob, ?docm) ^ hasOutputBearingScrewMatch(?dob, ?obs) ^ hasSlipRingTorqueSensorMatch(?dob, ?srtsm) ^ hasSlipRing(?srtsm, ?sr) ^ hasTorsionalWave(?dob, ?tw) ^ hasScrew(?docm, ?s_docm) ^ hasOutputBearing(?obs, ?ob) ^ hasScrew(?obs, ?s_ob) ^
hasDimensionOuterDiameter_D_unit_mm(?ob, ?D_ob) ^ hasDimensionOuterDiameter_D_unit_mm(?s_ob, ?D_s_ob) ^ hasDimensionInnerDiameter_d_unit_mm(?ob, ?d_ob) ^  hasDimensionOuterDiameter_D_unit_mm(?s_docm, ?D_s_docm) ^ hasDimensionOuterDiameter_D_unit_mm(?sr, ?D_sr) ^ hasDimensionOuterDiameter_D_unit_mm(?tw, ?D_tw) ^
swrlb:multiply(?D_s_ob2, 2.0, ?D_s_ob) ^ swrlb:add(?D_dob, ?D_ob, ?D_s_ob2) ^
swrlb:multiply(?D_s_docm2, 2.0, ?D_s_docm) ^ swrlb:multiply(?D_tw2, 2.0, ?D_tw) ^ swrlb:add(?temp1, ?d_ob, ?D_s_ob2) ^ swrlb:subtract(?temp2, ?temp1, ?D_s_docm2) ^ swrlb:subtract(?d_dob, ?temp2, ?D_tw2) ^ swrlb:greaterThanOrEqual(?d_dob, ?D_sr)
-> HalfSatisfiedDiameterOutputBearing(?dob) ^ hasDimensionOuterDiameter_D_unit_mm(?dob, ?D_dob) ^ hasDimensionInnerDiameter_d_unit_mm(?dob, ?d_dob)



DiameterOutputBearingPossibleUnsatisfied1Rule:
HalfSatisfiedDiameterOutputBearing(?dob) ^ hasDiameterOutputCircularScrewMatch(?dob, ?docm) ^ hasDiameterOutput(?docm, ?do)
hasDimensionOuterDiameter_D_unit_mm(?dob, ?D_dob) ^ hasDimensionOuterDiameter_D_unit_mm(?do, ?D_docm)
swrlb:greaterThan(?D_dob, ?D_docm)
-> PossibleUnsatisfiedDiameterOutputBearing(?dob)

DiameterOutputBearingPossibleUnsatisfied2Rule:
HalfSatisfiedDiameterOutputBearing(?dob) ^ hasDiameterOutputCircularScrewMatch(?dob, ?docm) ^ hasDiameterOutput(?docm, ?do)
hasDimensionOuterDiameter_D_unit_mm(?dob, ?D_dob) ^ hasDimensionOuterDiameter_D_unit_mm(?do, ?D_docm)
swrlb:lessThanOrEqual(?D_dob, ?D_docm)
-> HalfPossibleUnsatisfiedSatisfiedDiameterOutputBearing(?dob)

DiameterOutputBearingPossibleUnsatisfied3Rule:
HalfPossibleUnsatisfiedSatisfiedDiameterOutputBearing(?dob1) ^ HalfPossibleUnsatisfiedSatisfiedDiameterOutputBearing(?dob2) ^
hasDiameterOutputCircularScrewMatch(?dob1, ?docm) ^ hasSlipRingTorqueSensorMatch(?dob1, ?srtsm) ^ hasTorsionalWave(?dob1, ?tw) ^
hasDiameterOutputCircularScrewMatch(?dob2, ?docm) ^ hasSlipRingTorqueSensorMatch(?dob2, ?srtsm) ^ hasTorsionalWave(?dob2, ?tw) ^
hasDimensionOuterDiameter_D_unit_mm(?dob1, ?D_dob1) ^ hasDimensionOuterDiameter_D_unit_mm(?dob2, ?D_dob2) ^
swrlb:greaterThan(?D_dob1, ?D_dob2) ^ swrlb:lessThan(?D_dob2, ?D_dob1)
-> PossibleUnsatisfiedDiameterOutputBearing(?dob2)

DiameterOutputBearingPossibleSatisfiedRule:
HalfPossibleUnsatisfiedSatisfiedDiameterOutputBearing(?dob1) ^ HalfPossibleUnsatisfiedSatisfiedDiameterOutputBearing(?dob2) ^
hasDiameterOutputCircularScrewMatch(?dob1, ?docm) ^ hasSlipRingTorqueSensorMatch(?dob1, ?srtsm) ^ hasTorsionalWave(?dob1, ?tw) ^
hasDiameterOutputCircularScrewMatch(?dob2, ?docm) ^ hasSlipRingTorqueSensorMatch(?dob2, ?srtsm) ^ hasTorsionalWave(?dob2, ?tw) ^
hasDimensionOuterDiameter_D_unit_mm(?dob1, ?D_dob1) ^ hasDimensionOuterDiameter_D_unit_mm(?dob2, ?D_dob2) ^
swrlb:equal(?D_dob1, ?D_dob2)
-> PossibleSatisfiedDiameterOutputBearing(?dob1) ^ PossibleSatisfiedDiameterOutputBearing(?dob2)


DiameterOutputBearingBackupSatisfiedRule:
BackupSatisfiedDiameterOutputBearing(?dob1) ^ BackupSatisfiedDiameterOutputBearing(?dob2) ^
hasDiameterOutputCircularScrewMatch(?dob1, ?docm) ^ hasSlipRingTorqueSensorMatch(?dob1, ?srtsm) ^ hasTorsionalWave(?dob1, ?tw) ^
hasDiameterOutputCircularScrewMatch(?dob2, ?docm) ^ hasSlipRingTorqueSensorMatch(?dob2, ?srtsm) ^ hasTorsionalWave(?dob2, ?tw) ^
hasDimensionInnerDiameter_d_unit_mm(?dob1, ?d_dob1) ^ hasDimensionInnerDiameter_d_unit_mm(?dob2, ?d_dob2) ^
swrlb:greaterThan(?d_dob2, ?d_dob1) ^ swrlb:lessThan(?d_dob1, ?d_dob2)
-> UnsatisfiedDiameterOutputBearing(?dob2)

DiameterOutputBearingBackupPossibleSatisfiedRule:
BackupSatisfiedDiameterOutputBearing(?dob1) ^ BackupSatisfiedDiameterOutputBearing(?dob2) ^
hasDiameterOutputCircularScrewMatch(?dob1, ?docm) ^ hasSlipRingTorqueSensorMatch(?dob1, ?srtsm) ^ hasTorsionalWave(?dob1, ?tw) ^
hasDiameterOutputCircularScrewMatch(?dob2, ?docm) ^ hasSlipRingTorqueSensorMatch(?dob2, ?srtsm) ^ hasTorsionalWave(?dob2, ?tw) ^
hasDimensionInnerDiameter_d_unit_mm(?dob1, ?d_dob1) ^ hasDimensionInnerDiameter_d_unit_mm(?dob2, ?d_dob2) ^
swrlb:equal(?d_dob1, ?d_dob2)
-> BackupPossibleSatisfiedDiameterOutputBearing(?dob1) ^ BackupPossibleSatisfiedDiameterOutputBearing(?dob2)



DiameterEvaluate1Rule:
Diameter(?d) ^ hasLength(?d, ?l) ^ hasLengthOutput(?l, ?lo) ^ hasLengthInput(?lo, ?li) ^ hasDiameterInputDriveBearingMatch(?li, ?didbm) ^ hasDiameterInput(?didbm, ?di) ^ hasDiameterOutputBearing(?lo, ?dob) ^
hasDimensionOuterDiameter_D_unit_mm(?di, ?D_di) ^ hasDimensionOuterDiameter_D_unit_mm(?dob, ?D_dob) ^
swrlb:greaterThan(?D_di, ?D_dob)
-> has1(?d, ?D_di)

DiameterEvaluate2Rule:
Diameter(?d) ^ hasLength(?d, ?l) ^ hasLengthOutput(?l, ?lo) ^ hasLengthInput(?lo, ?li) ^ hasDiameterInputDriveBearingMatch(?li, ?didbm) ^ hasDiameterInput(?didbm, ?di) ^ hasDiameterOutputBearing(?lo, ?dob) ^
hasDimensionOuterDiameter_D_unit_mm(?di, ?D_di) ^ hasDimensionOuterDiameter_D_unit_mm(?dob, ?D_dob) ^
swrlb:lessThanOrEqual(?D_di, ?D_dob)
-> has1(?d, ?D_dob)

DiameterEvaluate3Rule:
Diameter(?d) ^ hasLength(?d, ?l) ^ hasLengthOutput(?l, ?lo) ^ hasDiameterOutputBearing(?lo, ?dob) ^  hasDiameterOutputCircularScrewMatch(?dob, ?docm) ^ hasDiameterOutput(?docm, ?do)
has1(?d, ?D_1) ^ hasDimensionOuterDiameter_D_unit_mm(?do, ?D_do) ^
swrlb:greaterThan(?D_1, ?D_do)
-> hasDimensionOuterDiameter_D_unit_mm(?d, ?D_1)

DiameterEvaluate4Rule:
Diameter(?d) ^ hasLength(?d, ?l) ^ hasLengthOutput(?l, ?lo) ^ hasDiameterOutputBearing(?lo, ?dob) ^  hasDiameterOutputCircularScrewMatch(?dob, ?docm) ^ hasDiameterOutput(?docm, ?do)
has1(?d, ?D_1) ^ hasDimensionOuterDiameter_D_unit_mm(?do, ?D_do) ^
swrlb:lessThanOrEqual(?D_1, ?D_do)
-> hasDimensionOuterDiameter_D_unit_mm(?d, ?D_do)


DiameterSatisfyRule:
Diameter(?d) ^ hasDimensionOuterDiameter_D_unit_mm(?d, ?D_ges) ^ CurrentRequirements(?r) ^ hasDimensionOuterDiameterReqMax_D_unit_mm(?r, ?D_req_max) ^ swrlb:lessThanOrEqual(?D_ges, ?D_req_max) ^ hasDimensionOuterDiameterReqMin_D_unit_mm(?r, ?D_req_min) ^ swrlb:greaterThanOrEqual(?D_ges, ?D_req_min) -> SatisfiedDiameter(?d)





LengthMotorGearBoxMatchSatisfyRule:
SACunit(?sac) ^ hasDiameter(?sac, ?d) ^ hasLength(?d, ?l) ^ hasLengthOutput(?l, ?lo) ^ hasLengthInput(?lo, ?li) ^ hasGearBox(?li, ?g) ^ hasDiameterInputDriveBearingMatch(?li, ?didbm) ^ hasDiameterInput(?didbm, ?di) ^ hasMotor(?di, ?m) ^
hasDimensionOuterDiameter_D_unit_mm(?d, ?D_res) ^ hasDimensionLength_L_unit_mm(?l, ?L_res)
SatisfiedMotorGearBoxMatchForSACunit(?ma) ^ hasMotor(?ma, ?m) ^ hasGearBox(?ma, ?g) ^ hasMaximalSpeedRes_n_max_unit_rpm(?ma, ?n_res) ^ hasPeakTorqueRes_M_max_unit_Nm(?ma, ?M_res)
-> SatisfiedRotationSpeed(?sac) ^ hasMaximalSpeedRes_n_max_unit_rpm(?sac, ?n_res) ^ SatisfiedPeakTorque(?sac) ^ hasPeakTorqueRes_M_max_unit_Nm(?sac, ?M_res) ^ SatisfiedOuterDiameter(?sac) ^ hasDimensionOuterDiameter_D_unit_mm(?sac, ?D_res) ^ SatisfiedDimensionLength(?sac) ^ hasDimensionLength_L_unit_mm(?sac, ?L_res)


SACunitWeightSatisfyRule:
SACunit(?sac) ^ hasDiameter(?sac, ?d) ^ hasLength(?d, ?l) ^ hasLengthOutput(?l, ?lo) ^ hasLengthInput(?lo, ?li) ^ hasDiameterInputDriveBearingMatch(?li, ?didbm) ^ hasDiameterInput(?didbm, ?di) ^ hasMotorController(?di, ?mc) ^ hasIncrementalEncoder(?lil, ?ie) ^ hasMotor(?di, ?m) ^ hasGearBox(?li, ?g) ^ hasDiameterOutputBearing(?lo, ?dob) ^ hasScrew(?lo, ?s_lo) ^ hasSlipRingTorqueSensorMatch(?dob, ?srtsm) ^ hasSlipRing(?srtsm, ?sr) ^ hasTorqueSensor(?srtsm, ?ts) ^ hasDiameterOutputCircularScrewMatch(?dob, ?gs) ^ hasScrew(?gs, ?s_gs) ^ hasDiameterOutput(?gs, ?do) ^ hasDriveBearing(?didbm, ?db) ^ hasGearBoxAbsolutEncoderSensorPCBMatch(?do, ?gaespcb) ^ hasAbsoluteEncoderSensorPCBMatch(?gaespcb, ?abspcb) ^ hasAbsoluteEncoder(?abspcb, ?ab) ^ hasSensorPCB(?abspcb, ?spcb) ^ hasOutputBearingScrewMatch(?dob, ?obs) ^ hasTorsionalWave(?dob, ?tw) ^ hasOutputBearing(?obs, ?ob) ^ hasScrew(?obs, ?s_ob) ^
hasWeight_m_unit_kg(?m, ?m_m) ^ hasWeight_m_unit_kg(?g, ?m_g) ^ hasWeight_m_unit_kg(?db, ?m_db) ^
swrlb:add(?temp1, ?m_m, ?m_g) ^ swrlb:add(?m_ges, ?temp1, ?m_db)
-> SatisfiedWeight(?sac) ^ hasWeight_m_unit_kg(?sac, ?m_ges)


SACunitSatisfyRule:
SACunit(?sac) ^ SatisfiedRotationSpeed(?sac) ^ SatisfiedPeakTorque(?sac) ^ SatisfiedDimensionLength(?sac) ^ SatisfiedOuterDiameter(?sac) ^ SatisfiedWeight(?sac) -> SatisfiedSACunit(?sac)



SACunitIsComposedOfRule:
SatisfiedSACunit(?sac) ^ hasDiameter(?sac, ?d) ^ hasLength(?d, ?l) ^ hasLengthOutput(?l, ?lo) ^ hasLengthInput(?lo, ?li) ^ hasDiameterInputDriveBearingMatch(?li, ?didbm) ^ hasDiameterInput(?didbm, ?di) ^ hasMotorController(?di, ?mc) ^ hasIncrementalEncoder(?lil, ?ie) ^ hasMotor(?di, ?m) ^ hasGearBox(?li, ?g) ^ hasDiameterOutputBearing(?lo, ?dob) ^ hasScrew(?lo, ?s_lo) ^ hasSlipRingTorqueSensorMatch(?dob, ?srtsm) ^ hasSlipRing(?srtsm, ?sr) ^ hasTorqueSensor(?srtsm, ?ts) ^ hasDiameterOutputCircularScrewMatch(?dob, ?gs) ^ hasScrew(?gs, ?s_gs) ^ hasDiameterOutput(?gs, ?do) ^ hasDriveBearing(?didbm, ?db) ^ hasGearBoxAbsolutEncoderSensorPCBMatch(?do, ?gaespcb) ^ hasAbsoluteEncoderSensorPCBMatch(?gaespcb, ?abspcb) ^ hasAbsoluteEncoder(?abspcb, ?ab) ^ hasSensorPCB(?abspcb, ?spcb) ^ hasOutputBearingScrewMatch(?dob, ?obs) ^ hasTorsionalWave(?dob, ?tw) ^ hasOutputBearing(?obs, ?ob) ^ hasScrew(?obs, ?s_ob)
-> isComposedOfIncrementalEncoder(?sac, ?ie) ^ isComposedOfMotor(?sac, ?m) ^ isComposedOfGearBox(?sac, ?g) ^ GearBoxCircularScrew(?s_gs) ^ isComposedOfGearBoxCircularScrew(?sac, ?s_gs) ^ GearBoxFlexScrew(?s_lo) ^  isComposedOfGearBoxFlexScrew(?sac, ?s_lo) ^ isComposedOfDriveBearing(?sac, ?db) ^ isComposedOfOutputBearing(?sac, ?ob) ^ OutputBearingScrew(?s_ob) ^ isComposedOfOutputBearingScrew(?sac, ?s_ob) ^ isComposedOfMotorController(?sac, ?mc) ^ isComposedOfSlipRing(?sac, ?sr) ^ isComposedOfTorqueSensor(?sac, ?ts) ^ isComposedOfTorsionalWave(?sac, ?tw) ^ isComposedOfAbsoluteEncoder(?sac, ?ab) ^ isComposedOfSensorPCB(?sac, ?spcb) ^ isComposedOfLengthInput(?sac, ?li)



