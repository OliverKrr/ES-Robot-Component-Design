MotorMaximalSpeedSatisfyRule:
Motor(?m) ^ hasNominalRotationSpeed_n_max_unit_rpm(?m, ?n_motor) ^ CurrentRequirements(?r) ^ hasMaximalSpeedReqMax_n_max_unit_rpm(?r, ?n_req_max) ^ swrlb:divide(?n_res_min, ?n_motor, 160.0) ^ swrlb:lessThanOrEqual(?n_res_min, ?n_req_max) ^ hasMaximalSpeedReqMin_n_max_unit_rpm(?r, ?n_req_min) ^ swrlb:divide(?n_res_max, ?n_motor, 1.0) ^ swrlb:greaterThanOrEqual(?n_res_max, ?n_req_min) -> SatisfiedRotationSpeed(?m)

MotorPeakTorqueSatisfyRule:
Motor(?m) ^ hasNominalTorque_M_n_unit_Nm(?m, ?M_n) ^ CurrentRequirements(?r) ^ hasPeakTorqueReqMax_M_max_unit_Nm(?r, ?M_req_max) ^ swrlb:multiply(?M_res_min, ?M_n, 1.0) ^ swrlb:lessThanOrEqual(?M_res_min, ?M_req_max) ^ swrlb:multiply(?M_res_max, ?M_n, 160.0) ^ hasPeakTorqueReqMin_M_max_unit_Nm(?r, ?M_req_min) ^ swrlb:greaterThanOrEqual(?M_res_max, ?M_req_min) -> SatisfiedPeakTorque(?m)

MotorWeightSatisfyRule:
Motor(?m) ^ hasWeight_m_unit_kg(?m, ?m_m) ^ CurrentRequirements(?r) ^ hasWeightReqMax_m_unit_kg(?r, ?m_req_max) ^ swrlb:add(?m_res_min, ?m_m, 0.0) ^ swrlb:lessThanOrEqual(?m_res_min, ?m_req_max) ^ hasWeightReqMin_m_unit_kg(?r, ?m_req_min) ^ swrlb:add(?m_res_max, ?m_m, 1.9) ^ swrlb:greaterThanOrEqual(?m_res_max, ?m_req_min) -> SatisfiedWeight(?m)


MotorSatisfyRule:
Motor(?m) ^ SatisfiedRotationSpeed(?m) ^ SatisfiedPeakTorque(?m) ^ SatisfiedWeight(?m) -> SatisfiedMotor(?m)



GearBoxMaximalSpeedSatisfyRule:
GearBox(?g) ^ hasGearBoxRation_i(?g, ?i) ^ CurrentRequirements(?r) ^ hasMaximalSpeedReqMax_n_max_unit_rpm(?r, ?n_req_max) ^ swrlb:divide(?n_res_min, 650.0, ?i) ^ swrlb:lessThanOrEqual(?n_res_min, ?n_req_max) ^ hasMaximalSpeedReqMin_n_max_unit_rpm(?r, ?n_req_min) ^ swrlb:divide(?n_res_max, 24000.0, ?i) ^ swrlb:greaterThanOrEqual(?n_res_max, ?n_req_min) ^ hasMaxRotationInputSpeedGrease_n_max_unit_rpm(?g, ?n_max) ^ swrlb:divide(?n_max_res, ?n_max, ?i) ^ swrlb:greaterThanOrEqual(?n_max_res, ?n_req_min) ^ swrlb:lessThanOrEqual(?n_max_res, ?n_req_max) -> SatisfiedRotationSpeed(?g)

GearBoxPeakTorqueSatisfyRule:
GearBox(?g) ^ hasGearBoxRation_i(?g, ?i) ^ CurrentRequirements(?r) ^ hasPeakTorqueReqMax_M_max_unit_Nm(?r, ?M_req_max) ^ swrlb:multiply(?M_res_min, 0.024, ?i) ^ swrlb:lessThanOrEqual(?M_res_min, ?M_req_max) ^ swrlb:multiply(?M_res_max, 11.2, ?i) ^ hasPeakTorqueReqMin_M_max_unit_Nm(?r, ?M_req_min) ^ swrlb:greaterThanOrEqual(?M_res_max, ?M_req_min) ^ hasPeakTorque_M_max_unit_Nm(?g, ?M_max) ^ swrlb:lessThanOrEqual(?M_res_min, ?M_max) ^ swrlb:lessThanOrEqual(?M_req_min, ?M_max) -> SatisfiedPeakTorque(?g)

GearBoxWeightSatisfyRule:
GearBox(?g) ^ hasWeight_m_unit_kg(?g, ?m_g) ^ CurrentRequirements(?r) ^ hasWeightReqMax_m_unit_kg(?r, ?m_req_max) ^ swrlb:add(?m_res_min, ?m_g, 0.016) ^ swrlb:lessThanOrEqual(?m_res_min, ?m_req_max) ^ swrlb:add(?m_res_max, ?m_g, 2.17) ^ hasWeightReqMin_m_unit_kg(?r, ?m_req_min) ^ swrlb:greaterThanOrEqual(?m_res_max, ?m_req_min) -> SatisfiedWeight(?g)


GearBoxSatisfyRule:
GearBox(?g) ^ SatisfiedRotationSpeed(?g) ^ SatisfiedPeakTorque(?g) ^ SatisfiedWeight(?g) -> SatisfiedGearBox(?g)




MotorGearBoxMatchMaximalSpeedCalcRule:
MotorGearBoxMatch(?ma) ^ hasMotor(?ma, ?m) ^ hasGearBox(?ma, ?g) ^ hasNominalRotationSpeed_n_max_unit_rpm(?m, ?n_motor) ^ hasGearBoxRation_i(?g, ?i) ^ swrlb:divide(?n_res, ?n_motor, ?i) -> hasMaximalSpeedRes_n_max_unit_rpm(?ma, ?n_res)

MotorGearBoxMatchMaximalSpeedSatisfyRule:
MotorGearBoxMatch(?ma) ^ hasGearBox(?ma, ?g) ^ hasMaxRotationInputSpeedGrease_n_max_unit_rpm(?g, ?n_max) ^ hasGearBoxRation_i(?g, ?i) ^ swrlb:divide(?n_max_res, ?n_max, ?i) ^ hasMaximalSpeedRes_n_max_unit_rpm(?ma, ?n_res) ^ CurrentRequirements(?r) ^ hasMaximalSpeedReqMax_n_max_unit_rpm(?r, ?n_req_max) ^ swrlb:lessThanOrEqual(?n_res, ?n_req_max) ^ hasMaximalSpeedReqMin_n_max_unit_rpm(?r, ?n_req_min) ^ swrlb:greaterThanOrEqual(?n_res, ?n_req_min) ^ swrlb:lessThanOrEqual(?n_max_res, ?n_req_max) ^ swrlb:greaterThanOrEqual(?n_max_res, ?n_req_min) -> SatisfiedRotationSpeed(?ma)


MotorGearBoxMatchPeakTorqueCalcRule:
MotorGearBoxMatch(?ma) ^ hasMotor(?ma, ?m) ^ hasGearBox(?ma, ?g) ^ hasNominalTorque_M_n_unit_Nm(?m, ?M_n) ^ hasGearBoxRation_i(?g, ?i) ^ swrlb:multiply(?M_res, ?M_n, ?i) -> hasPeakTorqueRes_M_max_unit_Nm(?ma, ?M_res)

MotorGearBoxMatchPeakTorqueSatisfyRule:
MotorGearBoxMatch(?ma) ^ hasGearBox(?ma, ?g) ^ hasPeakTorque_M_max_unit_Nm(?g, ?M_max) ^ hasPeakTorqueRes_M_max_unit_Nm(?ma, ?M_res) ^ swrlb:lessThanOrEqual(?M_res, ?M_max) ^ CurrentRequirements(?r) ^ hasPeakTorqueReqMax_M_max_unit_Nm(?r, ?M_req_max) ^ swrlb:lessThanOrEqual(?M_res, ?M_req_max) ^ hasPeakTorqueReqMin_M_max_unit_Nm(?r, ?M_req_min) ^ swrlb:greaterThanOrEqual(?M_res, ?M_req_min) -> SatisfiedPeakTorque(?ma)



MotorGearBoxMatchLengthCalcRule:
MotorGearBoxMatch(?ma) ^ hasMotor(?ma, ?m) ^ hasGearBox(?ma, ?g) ^ hasDimensionLength_L_unit_mm(?m, ?L_m) ^ hasDimensionLength_L_unit_mm(?g, ?L_g) ^ swrlb:add(?L_ges, ?L_m, ?L_g) -> hasDimensionLength_L_unit_mm(?ma, ?L_ges)

MotorGearBoxMatchLengthSatisfyRule:
MotorGearBoxMatch(?ma) ^ hasDimensionLength_L_unit_mm(?ma, ?L_ges) ^ CurrentRequirements(?r) ^ hasDimensionLengthReqMax_L_unit_mm(?r, ?L_req_max) ^ swrlb:lessThanOrEqual(?L_ges, ?L_req_max) ^ hasDimensionLengthReqMin_L_unit_mm(?r, ?L_req_min) ^ swrlb:greaterThanOrEqual(?L_ges, ?L_req_min) -> SatisfiedLength(?ma)


MotorGearBoxMatchOuterDiameter1CalcRule:
MotorGearBoxMatch(?ma) ^ hasMotor(?ma, ?m) ^ hasGearBox(?ma, ?g) ^ hasDimensionOuterDiameter_D_unit_mm(?m, ?D_m) ^ hasDimensionOuterDiameter_D_unit_mm(?g, ?D_g) ^ swrlb:greaterThan(?D_m, ?D_g) -> hasDimensionOuterDiameter_D_unit_mm(?ma, ?D_m)
MotorGearBoxMatchOuterDiameter2CalcRule:
MotorGearBoxMatch(?ma) ^ hasMotor(?ma, ?m) ^ hasGearBox(?ma, ?g) ^ hasDimensionOuterDiameter_D_unit_mm(?m, ?D_m) ^ hasDimensionOuterDiameter_D_unit_mm(?g, ?D_g) ^ swrlb:lessThanOrEqual(?D_m, ?D_g) -> hasDimensionOuterDiameter_D_unit_mm(?ma, ?D_g)

MotorGearBoxMatchOuterDiameterSatisfyRule:
MotorGearBoxMatch(?ma) ^ hasDimensionOuterDiameter_D_unit_mm(?ma, ?D_ges) ^ CurrentRequirements(?r) ^ hasDimensionOuterDiameterReqMax_D_unit_mm(?r, ?D_req_max) ^ swrlb:lessThanOrEqual(?D_ges, ?D_req_max) ^ hasDimensionOuterDiameterReqMin_D_unit_mm(?r, ?D_req_min) ^ swrlb:greaterThanOrEqual(?D_ges, ?D_req_min) -> SatisfiedOuterDiameter(?ma)



MotorGearBoxMatchWeightCalcRule:
MotorGearBoxMatch(?ma) ^ hasMotor(?ma, ?m) ^ hasGearBox(?ma, ?g) ^ hasWeight_m_unit_kg(?m, ?m_m) ^ hasWeight_m_unit_kg(?g, ?m_g) ^ swrlb:add(?m_ges, ?m_m, ?m_g) -> hasWeight_m_unit_kg(?ma, ?m_ges)

MotorGearBoxMatchWeightSatisfyRule:
MotorGearBoxMatch(?ma) ^ hasWeight_m_unit_kg(?ma, ?m_ges) ^ CurrentRequirements(?r) ^ hasWeightReqMax_m_unit_kg(?r, ?m_req_max) ^ swrlb:lessThanOrEqual(?m_ges, ?m_req_max) ^ hasWeightReqMin_m_unit_kg(?r, ?m_req_min) ^ swrlb:greaterThanOrEqual(?m_ges, ?m_req_min) -> SatisfiedWeight(?ma)



MotorGearBoxMatchSatisfyRule:
MotorGearBoxMatch(?ma) ^ SatisfiedRotationSpeed(?ma) ^ SatisfiedPeakTorque(?ma) ^ SatisfiedLength(?ma) ^ SatisfiedOuterDiameter(?ma) ^ SatisfiedWeight(?ma) ^ hasMotor(?ma, ?m) ^ hasGearBox(?ma, ?g) -> SatisfiedMotorGearBoxMatch(?ma) ^ SatisfiedMotorInMotorGearBoxMatch(?m) ^ SatisfiedGearBoxInMotorGearBoxMatch(?g) ^ isComposedOfMotor(?ma, ?m) ^ isComposedOfGearBox(?ma, ?g)




MotorControllerRadialSatisfyRule:
MCradial(?mc) ^ CurrentRequirements(?r) ^ needMotorControllerReq(?r, ?nmc) ^ swrlb:equal(?nmc, true) ^ Constants(?con) ^ 
lengthWiringConduit(?con, ?L_wc) ^ diameterElectronicsCover(?con, ?L_ec) ^ swrlb:add(?L_ges, ?L_wc, ?L_ec)
-> SatisfiedMotorController(?mc) ^ hasDimensionLengthElectric_L_uni_mm(?mc, ?L_ges)

MotorControllerFrontSatisfyRule:
MCfront(?mc) ^ CurrentRequirements(?r) ^ needMotorControllerReq(?r, ?nmc) ^ swrlb:equal(?nmc, true) ^ 
hasDimensionLength_L_unit_mm(?mc, ?L_mc)
-> SatisfiedMotorController(?mc) ^ hasDimensionLengthElectric_L_uni_mm(?mc, ?L_mc)

MotorControllerNoneSatisfyRule:
NoMC(?mc) ^ CurrentRequirements(?r) ^ needMotorControllerReq(?r, ?nmc) ^ swrlb:equal(?nmc, false) ^
-> SatisfiedMotorController(?mc) ^ hasDimensionLengthElectric_L_uni_mm(?mc, 0.0)



TorsionalWaveWithDMSSatisfyRule:
TorsionalWaveWithDMS(?twdms) ^ CurrentRequirements(?r) ^ needTorsionalWaveDMSReq(?r, ?ntwdms) ^ swrlb:equal(?ntwdms, true) -> SatisfiedTorsionalWaveDMS(?twdms)

TorsionalWaveNoDMSSatisfyRule:
TorsionalWaveNoDMS(?twdms) ^ CurrentRequirements(?r) ^ needTorsionalWaveDMSReq(?r, ?ntwdms) ^ swrlb:equal(?ntwdms, false) -> SatisfiedTorsionalWaveDMS(?twdms)



SlipRingNoneSatisfyRule:
NoSlipRingVariants(?sr) ^ CurrentRequirements(?r) ^ needContinuousRotationReq(?r, ?ncr) ^ swrlb:equal(?ncr, false) ^ hasDimensionDiameterWiringReq_D_unit_mm(?r, ?D) -> SatisfiedSlipRing(?sr) ^ hasDimensionOuterDiameter_D_unit_mm(?sr, ?D)


SlipRingSatisfy1Rule:
_1000MEthernet(?sr) ^ CurrentRequirements(?r) ^
has100MBASET(?sr, ?m100) ^ has1000MBASET(?sr, ?m1000) ^
needOutputCommunicationReq(?r, ?noc) ^ needContinuousRotationReq(?r, ?ncr) ^
swrlb:equal(?ncr, true) ^ swrlb:equal(?noc, true) ^ swrlb:add(?nc, ?m100, ?m1000) ^ swrlb:greaterThanOrEqual(?nc, 1)
-> SatisfiedSlipRing(?sr)

SlipRingSatisfy2Rule:
_1000MEthernet(?sr) ^ CurrentRequirements(?r) ^
needOutputCommunicationReq(?r, ?noc) ^ needContinuousRotationReq(?r, ?ncr) ^
swrlb:equal(?ncr, true) ^ swrlb:equal(?noc, false)
-> SatisfiedSlipRing(?sr)



TorqueSensorStrainGaugeBased_HollowShaftSatisfyRule:
StrainGaugeBased_HollowShaft(?ts) ^ CurrentRequirements(?r) ^
needTorqueSensorStrainGaugeBasedReq(?r, ?ntssgb) ^
swrlb:equal(?ntssgb, true)
-> SatisfiedTorqueSensor(?ts)


TorqueSensorNoneShaftSatisfyRule:
NoTorqueSensor(?ts) ^ CurrentRequirements(?r) ^
needTorqueSensorStrainGaugeBasedReq(?r, ?ntssgb) ^
swrlb:equal(?ntssgb, false)
-> SatisfiedTorqueSensor(?ts)



SlipRingTorqueSensorMatchSatisfy1Rule:
SlipRingTorqueSensorMatch(?srtsm) ^ hasSlipRing(?srtsm, ?sr) ^ _1000MEthernet(?sr) ^ hasTorqueSensor(?srtsm, ?ts) ^ CurrentRequirements(?r) ^
hasSignal_2A(?sr, ?s2a) ^ has10A(?sr, ?s10a) ^ has20A(?sr, ?s20a) ^ hasSignal_2A(?ts, ?s2a_ts) ^
hasCurrentOutputReqMax_I_unit_A(?r, ?co) ^ hasNumberOfOutputEmergencyStopReq(?r, ?nooes) ^ hasNumberOfOtherCablesReq(?r, ?nooc) ^
swrlb:subtract(?restSignal1, ?s2a, ?nooc) ^ swrlb:subtract(?restSignal2, ?restSignal1, ?nooes) ^ swrlb:subtract(?restSignal3, ?restSignal2, ?s2a_ts) ^ swrlb:multiply(?restSignalInA, ?restSignal3, 2.0) ^ swrlb:multiply(?s10aInA, ?s10a, 10.0) ^ swrlb:multiply(?s20aInA, ?s20a, 20.0) ^ swrlb:add(?sumInA1, ?restSignalInA, ?s10aInA) ^ swrlb:add(?sumInA2, ?sumInA1, ?s20aInA) ^ swrlb:divide(?sumHalf, ?sumInA2, 2.0) ^ swrlb:greaterThanOrEqual(?sumHalf, ?co)
-> HalfSatisfiedSlipRingTorqueSensorMatch(?srtsm)


SlipRingTorqueSensorMatchSatisfy2Rule:
SlipRingTorqueSensorMatch(?srtsm) ^ hasSlipRing(?srtsm, ?sr) ^ NoSlipRingVariants(?sr)
-> SatisfiedSlipRingTorqueSensorMatch(?srtsm)


SlipRingTorqueSensorMatchUnsatisfied1Rule:
HalfSatisfiedSlipRingTorqueSensorMatch(?srtsm1) ^ hasSlipRing(?srtsm1, ?sr1) ^ HalfSatisfiedSlipRingTorqueSensorMatch(?srtsm2) ^ hasSlipRing(?srtsm2, ?sr2) ^
hasTorqueSensor(?srtsm1, ?ts) ^ hasTorqueSensor(?srtsm2, ?ts) ^
hasDimensionLength_L_unit_mm(?sr1, ?L_sr1) ^ hasDimensionOuterDiameter_D_unit_mm(?sr1, ?D_sr1) ^ hasDimensionLength_L_unit_mm(?sr2, ?L_sr2) ^ hasDimensionOuterDiameter_D_unit_mm(?sr2, ?D_sr2) ^
swrlb:greaterThan(?L_sr1, ?L_sr2) ^ swrlb:lessThan(?L_sr2, ?L_sr1) ^ swrlb:greaterThan(?D_sr1, ?D_sr2) ^ swrlb:lessThan(?D_sr2, ?D_sr1)
-> UnsatisfiedSlipRingTorqueSensorMatch(?srtsm1)

SlipRingTorqueSensorMatchUnsatisfied2Rule:
HalfSatisfiedSlipRingTorqueSensorMatch(?srtsm1) ^ hasSlipRing(?srtsm1, ?sr1) ^ HalfSatisfiedSlipRingTorqueSensorMatch(?srtsm2) ^ hasSlipRing(?srtsm2, ?sr2) ^
hasTorqueSensor(?srtsm1, ?ts) ^ hasTorqueSensor(?srtsm2, ?ts) ^
hasDimensionLength_L_unit_mm(?sr1, ?L_sr1) ^ hasDimensionOuterDiameter_D_unit_mm(?sr1, ?D_sr1) ^ hasDimensionLength_L_unit_mm(?sr2, ?L_sr2) ^ hasDimensionOuterDiameter_D_unit_mm(?sr2, ?D_sr2) ^
swrlb:greaterThan(?L_sr1, ?L_sr2) ^ swrlb:lessThan(?L_sr2, ?L_sr1) ^ swrlb:equal(?D_sr2, ?D_sr1)
-> UnsatisfiedSlipRingTorqueSensorMatch(?srtsm1)

SlipRingTorqueSensorMatchUnsatisfied3Rule:
HalfSatisfiedSlipRingTorqueSensorMatch(?srtsm1) ^ hasSlipRing(?srtsm1, ?sr1) ^ HalfSatisfiedSlipRingTorqueSensorMatch(?srtsm2) ^ hasSlipRing(?srtsm2, ?sr2) ^
hasTorqueSensor(?srtsm1, ?ts) ^ hasTorqueSensor(?srtsm2, ?ts) ^
hasDimensionLength_L_unit_mm(?sr1, ?L_sr1) ^ hasDimensionOuterDiameter_D_unit_mm(?sr1, ?D_sr1) ^ hasDimensionLength_L_unit_mm(?sr2, ?L_sr2) ^ hasDimensionOuterDiameter_D_unit_mm(?sr2, ?D_sr2) ^
swrlb:equal(?L_sr2, ?L_sr1) ^ swrlb:greaterThan(?D_sr1, ?D_sr2) ^ swrlb:lessThan(?D_sr2, ?D_sr1)
-> UnsatisfiedSlipRingTorqueSensorMatch(?srtsm1)


SlipRingTorqueSensorMatchPossibleSatisfiedRule:
HalfSatisfiedSlipRingTorqueSensorMatch(?srtsm1) ^ hasSlipRing(?srtsm1, ?sr1) ^ HalfSatisfiedSlipRingTorqueSensorMatch(?srtsm2) ^ hasSlipRing(?srtsm2, ?sr2) ^
hasTorqueSensor(?srtsm1, ?ts) ^ hasTorqueSensor(?srtsm2, ?ts) ^
hasDimensionLength_L_unit_mm(?sr1, ?L_sr1) ^ hasDimensionOuterDiameter_D_unit_mm(?sr1, ?D_sr1) ^ hasDimensionLength_L_unit_mm(?sr2, ?L_sr2) ^ hasDimensionOuterDiameter_D_unit_mm(?sr2, ?D_sr2) ^
swrlb:equal(?L_sr2, ?L_sr1) ^ swrlb:equal(?D_sr2, ?D_sr1)
-> PossibleSatisfiedSlipRingTorqueSensorMatch(?srtsm1) ^ PossibleSatisfiedSlipRingTorqueSensorMatch(?srtsm2)





DiameterInputDriveBearingMatchSatisfyRule:
DiameterInputDriveBearingMatch(?didbm) ^ hasSACunitDiameterInput(?didbm, ?sacdi) ^ hasMotor(?sacdi, ?m) ^ hasDriveBearing(?didbm, ?db) ^ hasDimensionInnerDiameter_d_unit_mm(?m, ?d_m) ^ hasDimensionInnerDiameter_d_unit_mm(?db, ?d_ib) ^ swrlb:lessThanOrEqual(?d_ib, ?d_m) -> HalfSatisfiedDiameterInputDriveBearingMatch(?didbm)


DiameterInputDriveBearingMatchUnsatisfiedRule:
HalfSatisfiedDiameterInputDriveBearingMatch(?didbm1) ^ hasSACunitDiameterInput(?didbm1, ?sacdi1) ^ hasMotor(?sacdi1, ?m) ^ hasMotorController(?sacdi1, ?mc) ^ HalfSatisfiedDiameterInputDriveBearingMatch(?didbm2) ^ hasSACunitDiameterInput(?didbm2, ?sacdi2) ^ hasMotor(?sacdi2, ?m) ^ hasMotorController(?sacdi2, ?mc) ^ hasDriveBearing(?didbm1, ?db1) ^ hasDriveBearing(?didbm2, ?db2) ^ hasDimensionInnerDiameter_d_unit_mm(?db1, ?d_ib1) ^ hasDimensionInnerDiameter_d_unit_mm(?db2, ?d_ib2) ^ swrlb:greaterThan(?d_ib1, ?d_ib2) ^ swrlb:lessThan(?d_ib2, ?d_ib1) -> UnsatisfiedDiameterInputDriveBearingMatch(?didbm2)

DiameterInputDriveBearingMatchPossibleSatisfiedRule:
HalfSatisfiedDiameterInputDriveBearingMatch(?didbm1) ^ hasSACunitDiameterInput(?didbm1, ?sacdi1) ^ hasMotor(?sacdi1, ?m) ^ hasMotorController(?sacdi1, ?mc) ^ HalfSatisfiedDiameterInputDriveBearingMatch(?didbm2) ^ hasSACunitDiameterInput(?didbm2, ?sacdi2) ^ hasMotor(?sacdi2, ?m) ^ hasMotorController(?sacdi2, ?mc) ^ hasDriveBearing(?didbm1, ?db1) ^ hasDriveBearing(?didbm2, ?db2) ^ hasDimensionInnerDiameter_d_unit_mm(?db1, ?d_ib1) ^ hasDimensionInnerDiameter_d_unit_mm(?db2, ?d_ib2) ^ swrlb:equal(?d_ib2, ?d_ib1) -> PossibleSatisfiedDiameterInputDriveBearingMatch(?didbm1) ^ PossibleSatisfiedDiameterInputDriveBearingMatch(?didbm2)




IncrementalEncoderSatisfy1Rule:
NoIE(?ie) ^ CurrentRequirements(?r) ^ needIncrementalEncoderReq(?r, ?ier) ^ swrlb:equal(?ier, false) ^ needMotorControllerReq(?r, ?nmc) ^ swrlb:equal(?nmc, false) ^
hasDimensionLengthIncrementalEncoderConnector_L_unit_mm(?ie, ?L_iec) 
-> SatisfiedIncrementalEncoder(?ie) ^ has1(?ie, ?L_iec)

IncrementalEncoderSatisfy2Rule:
NoIE(?ie) ^ CurrentRequirements(?r) ^ needIncrementalEncoderReq(?r, ?ier) ^ swrlb:equal(?ier, false) ^ needMotorControllerReq(?r, ?nmc) ^ swrlb:equal(?nmc, true) ^
hasDimensionLengthIncrementalEncoderConnector_L_unit_mm(?ie, ?L_iec) 
-> SatisfiedIncrementalEncoder(?ie) ^ has1(?ie, 0.0)

IncrementalEncoderSatisfy3Rule:
WithIE(?ie) ^ CurrentRequirements(?r) ^ needIncrementalEncoderReq(?r, ?ier) ^ swrlb:equal(?ier, true) ^ needMotorControllerReq(?r, ?nmc) ^ swrlb:equal(?nmc, false) ^
hasDimensionLengthIncrementalEncoderConnector_L_unit_mm(?ie, ?L_iec) 
-> SatisfiedIncrementalEncoder(?ie) ^ has1(?ie, ?L_iec)

IncrementalEncoderSatisfy4Rule:
WithIE(?ie) ^ CurrentRequirements(?r) ^ needIncrementalEncoderReq(?r, ?ier) ^ swrlb:equal(?ier, true) ^ needMotorControllerReq(?r, ?nmc) ^ swrlb:equal(?nmc, true) ^
hasDimensionLengthIncrementalEncoderConnector_L_unit_mm(?ie, ?L_iec) 
-> SatisfiedIncrementalEncoder(?ie) ^ has1(?ie, 0.0)




SACunitLengthInputLinearCalcRule:
SACunitLengthInputLinear(?saclil) ^ hasDiameterInputDriveBearingMatch(?saclil, ?didbm) ^ hasIncrementalEncoder(?saclil, ?ie) ^ hasGearBox(?saclil, ?g) ^ hasDriveBearing(?didbm, ?db) ^ hasSACunitDiameterInput(?didbm, ?sacdi) ^ hasMotor(?sacdi, ?m) ^ hasMotorController(?sacdi, ?mc) ^
SatisfiedMotorGearBoxMatch(?ma) ^ hasMotor(?ma, ?m) ^ hasGearBox(?ma, ?g) ^
hasDimensionLength_L_unit_mm(?ie, ?L_ie) ^ has1(?ie, ?L_iec) ^ hasDimensionLength_L_unit_mm(?m, ?L_m) ^ hasDimensionLength_L_unit_mm(?db, ?L_ib) ^ Constants(?con) ^ lengthFloatBearingOffset(?con, ?fbo_con) ^ lengthMotorCover(?con, ?mc_con) ^ lengthMotorWiring(?con, ?mw_con) ^ hasDimensionSafety_c_unit_mm(?g, ?c_g) ^ hasDimensionLengthElectric_L_uni_mm(?mc, ?L_e) ^
swrlb:multiply(?L_fixedB, ?L_ib, 2.0) ^ swrlb:add(?L_temp1, ?L_m, ?L_fixedB) ^ swrlb:add(?L_temp2, ?L_temp1, ?L_e) ^ swrlb:add(?L_temp3, ?L_temp2, ?L_iec) ^ swrlb:add(?L_1, ?L_temp3, ?c_g) ^ swrlb:add(?L_floatB, ?L_ib, ?fbo_con) ^ swrlb:add(?L_max1, ?L_floatB, ?L_ie) ^ swrlb:add(?L_max2, ?mc_con, ?mw_con)
-> has1(?saclil, ?L_1) ^ hasMax1_1(?saclil, ?L_max1) ^ hasMax1_2(?saclil, ?L_max2) ^ SatisfiedSACunitLengthInputLinear(?saclil)




SACunitLengthInputLinearEvaluate1Rule:
SatisfiedSACunitLengthInputLinear(?saclil) ^ has1(?saclil, ?L_1) ^ hasMax1_1(?saclil, ?L_max1) ^ hasMax1_2(?saclil, ?L_max2) ^ swrlb:greaterThan(?L_max1, ?L_max2) ^ swrlb:add(?L_ges, ?L_1, ?L_max1) -> hasDimensionLength_L_unit_mm(?saclil, ?L_ges)

SACunitLengthInputLinearEvaluate2Rule:
SatisfiedSACunitLengthInputLinear(?saclil) ^ has1(?saclil, ?L_1) ^ hasMax1_1(?saclil, ?L_max1) ^ hasMax1_2(?saclil, ?L_max2) ^ swrlb:lessThanOrEqual(?L_max1, ?L_max2) ^ swrlb:add(?L_ges, ?L_1, ?L_max2) -> hasDimensionLength_L_unit_mm(?saclil, ?L_ges)




//TODO verschiedene AbsolutEncoder
AbsoluteEncoderSensorPCBMatchSatisfy1Rule:
sac_basic:NoAeNoPCB(?aespcbm) ^ CurrentRequirements(?r) ^ needAbsoluteEncoderReq(?r, ?ab) ^ swrlb:equal(?ab, false) ^ needSensorControlBoardReq(?r, ?spcm) ^ swrlb:equal(?spcm, false) -> SatisfiedAbsoluteEncoderSensorPCBMatch(?aespcbm)

AbsoluteEncoderSensorPCBMatchSatisfy2Rule:
sac_basic:NoAeWithPCB(?aespcbm) ^ CurrentRequirements(?r) ^ needAbsoluteEncoderReq(?r, ?ab) ^ swrlb:equal(?ab, false) ^ needSensorControlBoardReq(?r, ?spcm) ^ swrlb:equal(?spcm, true) -> SatisfiedAbsoluteEncoderSensorPCBMatch(?aespcbm)

AbsoluteEncoderSensorPCBMatchSatisfy3Rule:
sac_basic:WithAeNoPCB(?aespcbm) ^ CurrentRequirements(?r) ^ needAbsoluteEncoderReq(?r, ?ab) ^ swrlb:equal(?ab, true) ^ needSensorControlBoardReq(?r, ?spcm) ^ swrlb:equal(?spcm, false) -> SatisfiedAbsoluteEncoderSensorPCBMatch(?aespcbm)

AbsoluteEncoderSensorPCBMatchSatisfy4Rule:
sac_basic:WithAeWithPCB(?aespcbm) ^ CurrentRequirements(?r) ^ needAbsoluteEncoderReq(?r, ?ab) ^ swrlb:equal(?ab, true) ^ needSensorControlBoardReq(?r, ?spcm) ^ swrlb:equal(?spcm, true) -> SatisfiedAbsoluteEncoderSensorPCBMatch(?aespcbm)





DiameterOutputScrewMatchSatisfyRule:
DiameterOutputScrewMatch(?gs) ^ hasSACunitDiameterOutput(?gs, ?sacdo) ^ hasScrew(?gs, ?s) ^ hasGearBox(?sacdo, ?g) ^ hasDimensionScrewInnerDiameterFlexSplineM_d_unit_mm(?g, ?d_s1) ^ hasDimensionScrewInnerDiameterM_d_unit_mm(?s, ?d_s2) ^ swrlb:equal(?d_s1, ?d_s2) -> SatisfiedDiameterOutputScrewMatch(?gs)


OutputBearingScrewMatchSatisfyRule:
OutputBearingScrewMatch(?obs) ^ hasOutputBearing(?obs, ?ob) ^ hasScrew(?obs, ?s) ^ hasDimensionScrewInnerDiameterM_d_unit_mm(?ob, ?d_s1) ^ hasDimensionScrewInnerDiameterM_d_unit_mm(?s, ?d_s2) ^ swrlb:equal(?d_s1, ?d_s2) -> SatisfiedOutputBearingScrewMatch(?obs)



//TODO Srew aufteilen in Circular (diameter) und Flexspline (length)
// -> dadurch auch zwei isComposedOf...
// und auch Name mit Match umbennen mit jeweiliger Schraubenart
// und dann LÃ¤nge weiter testen, siehe Anfang auf Blatt

SACunitLengthOutputLinearCalcRule:
SACunitLengthOutputLinear(?saclol) ^ hasSACunitDiameterOutputBearing(?saclol, ?sacdob) ^ hasDiameterOutputScrewMatch(?sacdob, ?gs) ^ hasOutputBearingScrewMatch(?sacdob, ?obs) ^ hasSlipRingTorqueSensorMatch(?sacdob, ?srtsm) ^ hasTorqueSensor(?srtsm, ?ts) ^ hasSACunitDiameterOutput(?gs, ?sacdo) ^ hasAbsoluteEncoderSensorPCBMatch(?sacdo, ?aespcbm) ^ hasGearBox(?sacdo, ?g) ^ hasScrew(?gs, ?s_gs) ^ hasOutputBearing(?obs, ?ob) ^ hasScrew(?obs, ?s_ob) ^ Constants(?con) ^
hasDimensionLength_L_unit_mm(?g, ?L_g) ^ hasDimensionLengthScrewHead_k_unit_mm(?s_gs, ?L_s_gs) ^ hasDimensionScrewInnerDiameterM_d_unit_mm(?s_gs, ?d_s_gs) ^ hasDimensionLength_L_unit_mm(?ob, ?L_ob) ^ hasDimensionLengthScrewHead_k_unit_mm(?s_ob, ?L_s_ob) ^ hasDimensionLength_L_unit_mm(?aespcbm, ?L_loie) ^ hasDimensionLength_L_unit_mm(?ts, ?L_ts) ^ lengthCentering(?con, ?c_con) ^ lengthNotch(?con, ?n_con) ^ lengthMeasurementWidth(?con, ?mw_con) ^
swrlb:multiply(?L_2kerbe, 2.0, ?n_con) ^ swrlb:add(?L_torWelleTemp1, ?mw_con, ?L_2kerbe) ^ swrlb:multiply(?L_torWelleTemp2, ?d_s_gs, 1.5) ^ swrlb:multiply(?L_torWelleTemp3, 2.0, ?L_torWelleTemp2) ^ swrlb:add(?L_torWelle, ?L_torWelleTemp1, ?L_torWelleTemp3) ^ swrlb:add(?L_max1_1, ?L_torWelle, ?L_s_gs) ^ swrlb:multiply(?L_abLagerTemp1, 1.5, ?L_ob) ^ swrlb:add(?L_abLager, ?L_abLagerTemp1, ?c_con) ^ swrlb:add(?L_max1_2, ?L_loie, ?L_abLager) ^ swrlb:add(?L_max2_1, ?L_ts, ?L_s_gs) ^ swrlb:multiply(?L_max2_2, 2.0, ?L_s_ob)
-> hasMax1_1(?saclol, ?L_max1_1) ^ hasMax1_2(?saclol, ?L_max1_2) ^ hasMax2_1(?saclol, ?L_max2_1) ^ hasMax2_2(?saclol, ?L_max2_2) ^ has1(?saclol, ?L_g)


SACunitLengthOutputLinearEvaluate1Rule:
SACunitLengthOutputLinear(?saclol) ^ hasMax1_1(?saclol, ?L_max1_1) ^ hasMax1_2(?saclol, ?L_max1_2) ^ swrlb:greaterThan(?L_max1_1, ?L_max1_2) -> has2(?saclol, ?L_max1_1)

SACunitLengthOutputLinearEvaluate2Rule:
SACunitLengthOutputLinear(?saclol) ^ hasMax1_1(?saclol, ?L_max1_1) ^ hasMax1_2(?saclol, ?L_max1_2) ^ swrlb:lessThanOrEqual(?L_max1_1, ?L_max1_2) -> has2(?saclol, ?L_max1_2)

SACunitLengthOutputLinearEvaluate3Rule:
SACunitLengthOutputLinear(?saclol) ^ has1(?saclol, ?L_part1) ^ has2(?saclol, ?L_part2) ^ hasMax2_1(?saclol, ?L_max2_1) ^ hasMax2_2(?saclol, ?L_max2_2) ^ swrlb:greaterThan(?L_max2_1, ?L_max2_2) ^ swrlb:add(?L_ges_temp, ?L_part1, ?L_part2) ^ swrlb:add(?L_ges, ?L_ges_temp, ?L_max2_1) -> hasDimensionLength_L_unit_mm(?saclol, ?L_ges) ^ SatisfiedSACunitLengthOutputLinear(?saclol)

SACunitLengthOutputLinearEvaluate4Rule:
SACunitLengthOutputLinear(?saclol) ^ has1(?saclol, ?L_part1) ^ has2(?saclol, ?L_part2) ^ hasMax2_1(?saclol, ?L_max2_1) ^ hasMax2_2(?saclol, ?L_max2_2) ^ swrlb:lessThanOrEqual(?L_max2_1, ?L_max2_2) ^ swrlb:add(?L_ges_temp, ?L_part1, ?L_part2) ^ swrlb:add(?L_ges, ?L_ges_temp, ?L_max2_2) -> hasDimensionLength_L_unit_mm(?saclol, ?L_ges) ^ SatisfiedSACunitLengthOutputLinear(?saclol)



SACunitLengthCalcRule:
SACunitLength(?sacl) ^ hasSACunitLengthInput(?sacl, ?sacli) ^ hasSACunitLengthOutput(?sacl, ?saclo) ^ hasGearBox(?sacli, ?g) ^ hasSACunitDiameterOutputBearing(?saclo, ?sacdob) ^ hasDiameterOutputScrewMatch(?sacdob, ?docm) ^ hasSACunitDiameterOutput(?docm, ?sacdo) ^ hasGearBox(?sacdo, ?g) ^
hasDimensionLength_L_unit_mm(?sacli, ?L_sacli) ^ hasDimensionLength_L_unit_mm(?saclo, ?L_saclo) ^ swrlb:add(?L_ges, ?L_sacli, ?L_saclo)
-> hasDimensionLength_L_unit_mm(?sacl, ?L_ges)


SACunitLengthSatisfyRule:
SACunitLength(?sacl) ^ hasDimensionLength_L_unit_mm(?sacl, ?L_ges) ^ CurrentRequirements(?r) ^ hasDimensionLengthReqMax_L_unit_mm(?r, ?L_req_max) ^ swrlb:lessThanOrEqual(?L_ges, ?L_req_max) ^ hasDimensionLengthReqMin_L_unit_mm(?r, ?L_req_min) ^ swrlb:greaterThanOrEqual(?L_ges, ?L_req_min) -> SatisfiedSACunitLength(?sacl)






SACunitDiameterInputCalcRule:
SACunitDiameterInput(?sacdi) ^ hasMotor(?sacdi, ?m) ^ hasMotorController(?sacdi, ?mc) ^ hasDimensionOuterDiameter_D_unit_mm(?m, ?D_m) ^ hasDimensionOuterDiameter_D_unit_mm(?mc, ?D_mc) ^ CurrentRequirements(?r) ^ hasThicknessEngineCaseReq_D_unit_mm(?r, ?tec_r) ^ swrlb:multiply(?dec, ?tec_r, 2.0) ^ swrlb:multiply(?dmc, ?D_mc, 2.0) ^ swrlb:add(?temp, ?dec, ?dmc) ^ swrlb:add(?d_input, ?temp, ?D_m) -> hasDimensionOuterDiameter_D_unit_mm(?sacdi, ?d_input)



SACunitDiameterInputSatisfyRule:
SACunitDiameterInput(?sacdi) ^ hasDimensionOuterDiameter_D_unit_mm(?sacdi, ?D_input) ^ CurrentRequirements(?r) ^ hasDimensionOuterDiameterReqMax_D_unit_mm(?r, ?D_req_max) ^ swrlb:lessThanOrEqual(?D_input, ?D_req_max) -> SatisfiedSACunitDiameterInput(?sacdi)




SACunitDiameterOutputCalcRule:
SACunitDiameterOutput(?sacdo) ^ hasGearBox(?sacdo, ?g) ^ hasAbsoluteEncoderSensorPCBMatch(?sacdo, ?aespcbm) ^ Constants(?con) ^ hasDimensionOuterDiameter_D_unit_mm(?g, ?D_g) ^ diameterWiring(?con, ?D_w) ^ hasDimensionScrewInnerDiameterCircularSplineM_d_unit_mm(?g, ?D_scp) ^ hasDimensionOuterDiameter_D_unit_mm(?aespcbm, ?D_aespcbm) ^ diameterElectronicsCover(?con, ?D_ec) ^ swrlb:multiply(?D_w2, ?D_w, 2.0) ^ swrlb:multiply(?D_scp2, ?D_scp, 2.0) ^ swrlb:add(?temp1, ?D_g, ?D_w2) ^ swrlb:add(?D_max1, ?temp1, ?D_scp2) ^ swrlb:multiply(?D_ec2, ?D_ec, 2.0) ^ swrlb:add(?D_max2, ?D_ec2, ?D_aespcbm) -> hasMax1_1(?sacdo, ?D_max1) ^ hasMax1_2(?sacdo, ?D_max2)



SACunitDiameterOutputEvaluate1Rule:
SACunitDiameterOutput(?sacdo) ^ hasMax1_1(?sacdo, ?D_max1) ^ hasMax1_2(?sacdo, ?D_max2) ^ swrlb:greaterThan(?D_max1, ?D_max2) -> hasDimensionOuterDiameter_D_unit_mm(?sacdo, ?D_max1)

SACunitDiameterOutputEvaluate2Rule:
SACunitDiameterOutput(?sacdo) ^ hasMax1_1(?sacdo, ?D_max1) ^ hasMax1_2(?sacdo, ?D_max2) ^ swrlb:lessThanOrEqual(?D_max1, ?D_max2) -> hasDimensionOuterDiameter_D_unit_mm(?sacdo, ?D_max2)



SACunitDiameterOutputSatisfyRule:
SACunitDiameterOutput(?sacdo) ^ hasDimensionOuterDiameter_D_unit_mm(?sacdo, ?D_out) ^ CurrentRequirements(?r) ^ hasDimensionOuterDiameterReqMax_D_unit_mm(?r, ?D_req_max) ^ swrlb:lessThanOrEqual(?D_out, ?D_req_max) -> SatisfiedSACunitDiameterOutput(?sacdo)




SACunitDiameterOutputBearingCalcRule:
SACunitDiameterOutputBearing(?sacdob) ^ hasDiameterOutputScrewMatch(?sacdob, ?docm) ^ hasOutputBearingScrewMatch(?sacdob, ?obs) ^ hasSlipRingTorqueSensorMatch(?sacdob, ?srtsm) ^ hasSlipRing(?srtsm, ?sr) ^ hasTorsionalWaveDMS(?sacdob, ?twdms) ^ hasScrew(?docm, ?s_docm) ^ hasOutputBearing(?obs, ?ob) ^ hasScrew(?obs, ?s_ob) ^
hasDimensionOuterDiameter_D_unit_mm(?ob, ?D_ob) ^ hasDimensionOuterDiameter_D_unit_mm(?s_ob, ?D_s_ob) ^ hasDimensionInnerDiameter_d_unit_mm(?ob, ?d_ob) ^  hasDimensionOuterDiameter_D_unit_mm(?s_docm, ?D_s_docm) ^ hasDimensionOuterDiameter_D_unit_mm(?sr, ?D_sr) ^ hasDimensionOuterDiameter_D_unit_mm(?twdms, ?D_twdms) ^
swrlb:multiply(?D_s_ob2, 2.0, ?D_s_ob) ^ swrlb:add(?D_sacdob, ?D_ob, ?D_s_ob2) ^
swrlb:multiply(?D_s_docm2, 2.0, ?D_s_docm) ^ swrlb:multiply(?D_twdms2, 2.0, ?D_twdms) ^ swrlb:add(?temp1, ?d_ob, ?D_s_ob2) ^ swrlb:subtract(?temp2, ?temp1, ?D_s_docm2) ^ swrlb:subtract(?d_sacdob, ?temp2, ?D_twdms2) ^ swrlb:greaterThanOrEqual(?d_sacdob, ?D_sr)
-> HalfSatisfiedSACunitDiameterOutputBearing(?sacdob) ^ hasDimensionOuterDiameter_D_unit_mm(?sacdob, ?D_sacdob) ^ hasDimensionInnerDiameter_d_unit_mm(?sacdob, ?d_sacdob)



SACunitDiameterOutputBearingPossibleUnsatisfied1Rule:
HalfSatisfiedSACunitDiameterOutputBearing(?sacdob) ^ hasDiameterOutputScrewMatch(?sacdob, ?docm) ^ hasSACunitDiameterOutput(?docm, ?sacdo)
hasDimensionOuterDiameter_D_unit_mm(?sacdob, ?D_sacdob) ^ hasDimensionOuterDiameter_D_unit_mm(?sacdo, ?D_docm)
swrlb:greaterThan(?D_sacdob, ?D_docm)
-> PossibleUnsatisfiedSACunitDiameterOutputBearing(?sacdob)

SACunitDiameterOutputBearingPossibleUnsatisfied2Rule:
HalfSatisfiedSACunitDiameterOutputBearing(?sacdob) ^ hasDiameterOutputScrewMatch(?sacdob, ?docm) ^ hasSACunitDiameterOutput(?docm, ?sacdo)
hasDimensionOuterDiameter_D_unit_mm(?sacdob, ?D_sacdob) ^ hasDimensionOuterDiameter_D_unit_mm(?sacdo, ?D_docm)
swrlb:lessThanOrEqual(?D_sacdob, ?D_docm)
-> HalfPossibleUnsatisfiedSatisfiedSACunitDiameterOutputBearing(?sacdob)

SACunitDiameterOutputBearingPossibleUnsatisfied3Rule:
HalfPossibleUnsatisfiedSatisfiedSACunitDiameterOutputBearing(?sacdob1) ^ HalfPossibleUnsatisfiedSatisfiedSACunitDiameterOutputBearing(?sacdob2) ^
hasDiameterOutputScrewMatch(?sacdob1, ?docm) ^ hasSlipRingTorqueSensorMatch(?sacdob1, ?srtsm) ^ hasTorsionalWaveDMS(?sacdob1, ?twdms) ^
hasDiameterOutputScrewMatch(?sacdob2, ?docm) ^ hasSlipRingTorqueSensorMatch(?sacdob2, ?srtsm) ^ hasTorsionalWaveDMS(?sacdob2, ?twdms) ^
hasDimensionOuterDiameter_D_unit_mm(?sacdob1, ?D_sacdob1) ^ hasDimensionOuterDiameter_D_unit_mm(?sacdob2, ?D_sacdob2) ^
swrlb:greaterThan(?D_sacdob1, ?D_sacdob2) ^ swrlb:lessThan(?D_sacdob2, ?D_sacdob1)
-> PossibleUnsatisfiedSACunitDiameterOutputBearing(?sacdob2)

SACunitDiameterOutputBearingPossibleSatisfiedRule:
HalfPossibleUnsatisfiedSatisfiedSACunitDiameterOutputBearing(?sacdob1) ^ HalfPossibleUnsatisfiedSatisfiedSACunitDiameterOutputBearing(?sacdob2) ^
hasDiameterOutputScrewMatch(?sacdob1, ?docm) ^ hasSlipRingTorqueSensorMatch(?sacdob1, ?srtsm) ^ hasTorsionalWaveDMS(?sacdob1, ?twdms) ^
hasDiameterOutputScrewMatch(?sacdob2, ?docm) ^ hasSlipRingTorqueSensorMatch(?sacdob2, ?srtsm) ^ hasTorsionalWaveDMS(?sacdob2, ?twdms) ^
hasDimensionOuterDiameter_D_unit_mm(?sacdob1, ?D_sacdob1) ^ hasDimensionOuterDiameter_D_unit_mm(?sacdob2, ?D_sacdob2) ^
swrlb:equal(?D_sacdob1, ?D_sacdob2)
-> PossibleSatisfiedSACunitDiameterOutputBearing(?sacdob1) ^ PossibleSatisfiedSACunitDiameterOutputBearing(?sacdob2)


SACunitDiameterOutputBearingBackupSatisfiedRule:
BackupSatisfiedSACunitDiameterOutputBearing(?sacdob1) ^ BackupSatisfiedSACunitDiameterOutputBearing(?sacdob2) ^
hasDiameterOutputScrewMatch(?sacdob1, ?docm) ^ hasSlipRingTorqueSensorMatch(?sacdob1, ?srtsm) ^ hasTorsionalWaveDMS(?sacdob1, ?twdms) ^
hasDiameterOutputScrewMatch(?sacdob2, ?docm) ^ hasSlipRingTorqueSensorMatch(?sacdob2, ?srtsm) ^ hasTorsionalWaveDMS(?sacdob2, ?twdms) ^
hasDimensionInnerDiameter_d_unit_mm(?sacdob1, ?d_sacdob1) ^ hasDimensionInnerDiameter_d_unit_mm(?sacdob2, ?d_sacdob2) ^
swrlb:greaterThan(?d_sacdob2, ?d_sacdob1) ^ swrlb:lessThan(?d_sacdob1, ?d_sacdob2)
-> UnsatisfiedSACunitDiameterOutputBearing(?sacdob2)

SACunitDiameterOutputBearingBackupPossibleSatisfiedRule:
BackupSatisfiedSACunitDiameterOutputBearing(?sacdob1) ^ BackupSatisfiedSACunitDiameterOutputBearing(?sacdob2) ^
hasDiameterOutputScrewMatch(?sacdob1, ?docm) ^ hasSlipRingTorqueSensorMatch(?sacdob1, ?srtsm) ^ hasTorsionalWaveDMS(?sacdob1, ?twdms) ^
hasDiameterOutputScrewMatch(?sacdob2, ?docm) ^ hasSlipRingTorqueSensorMatch(?sacdob2, ?srtsm) ^ hasTorsionalWaveDMS(?sacdob2, ?twdms) ^
hasDimensionInnerDiameter_d_unit_mm(?sacdob1, ?d_sacdob1) ^ hasDimensionInnerDiameter_d_unit_mm(?sacdob2, ?d_sacdob2) ^
swrlb:equal(?d_sacdob1, ?d_sacdob2)
-> BackupPossibleSatisfiedSACunitDiameterOutputBearing(?sacdob1) ^ BackupPossibleSatisfiedSACunitDiameterOutputBearing(?sacdob2)



SACunitDiameterEvaluate1Rule:
SACunitDiameter(?sacd) ^ hasSACunitLength(?sacd, ?sacl) ^ hasSACunitLengthInput(?sacl, ?sacli) ^ hasDiameterInputDriveBearingMatch(?sacli, ?didbm) ^ hasSACunitDiameterInput(?didbm, ?sacdi) ^ hasSACunitLengthOutput(?sacl, ?saclo) ^ hasSACunitDiameterOutputBearing(?saclo, ?sacdob) ^
hasDimensionOuterDiameter_D_unit_mm(?sacdi, ?D_sacdi) ^ hasDimensionOuterDiameter_D_unit_mm(?sacdob, ?D_sacdob) ^
swrlb:greaterThan(?D_sacdi, ?D_sacdob)
-> has1(?sacd, ?D_sacdi)

SACunitDiameterEvaluate2Rule:
SACunitDiameter(?sacd) ^ hasSACunitLength(?sacd, ?sacl) ^ hasSACunitLengthInput(?sacl, ?sacli) ^ hasDiameterInputDriveBearingMatch(?sacli, ?didbm) ^ hasSACunitDiameterInput(?didbm, ?sacdi) ^ hasSACunitLengthOutput(?sacl, ?saclo) ^ hasSACunitDiameterOutputBearing(?saclo, ?sacdob) ^
hasDimensionOuterDiameter_D_unit_mm(?sacdi, ?D_sacdi) ^ hasDimensionOuterDiameter_D_unit_mm(?sacdob, ?D_sacdob) ^
swrlb:lessThanOrEqual(?D_sacdi, ?D_sacdob)
-> has1(?sacd, ?D_sacdob)

SACunitDiameterEvaluate3Rule:
SACunitDiameter(?sacd) ^ hasSACunitLength(?sacd, ?sacl) ^ hasSACunitLengthOutput(?sacl, ?saclo) ^ hasSACunitDiameterOutputBearing(?saclo, ?sacdob) ^  hasDiameterOutputScrewMatch(?sacdob, ?docm) ^ hasSACunitDiameterOutput(?docm, ?sacdo)
has1(?sacd, ?D_1) ^ hasDimensionOuterDiameter_D_unit_mm(?sacdo, ?D_sacdo) ^
swrlb:greaterThan(?D_1, ?D_sacdo)
-> hasDimensionOuterDiameter_D_unit_mm(?sacd, ?D_1)

SACunitDiameterEvaluate4Rule:
SACunitDiameter(?sacd) ^ hasSACunitLength(?sacd, ?sacl) ^ hasSACunitLengthOutput(?sacl, ?saclo) ^ hasSACunitDiameterOutputBearing(?saclo, ?sacdob) ^  hasDiameterOutputScrewMatch(?sacdob, ?docm) ^ hasSACunitDiameterOutput(?docm, ?sacdo)
has1(?sacd, ?D_1) ^ hasDimensionOuterDiameter_D_unit_mm(?sacdo, ?D_sacdo) ^
swrlb:lessThanOrEqual(?D_1, ?D_sacdo)
-> hasDimensionOuterDiameter_D_unit_mm(?sacd, ?D_sacdo)


SACunitDiameterSatisfyRule:
SACunitDiameter(?sacd) ^ hasDimensionOuterDiameter_D_unit_mm(?sacd, ?D_ges) ^ CurrentRequirements(?r) ^ hasDimensionOuterDiameterReqMax_D_unit_mm(?r, ?D_req_max) ^ swrlb:lessThanOrEqual(?D_ges, ?D_req_max) ^ hasDimensionOuterDiameterReqMin_D_unit_mm(?r, ?D_req_min) ^ swrlb:greaterThanOrEqual(?D_ges, ?D_req_min) -> SatisfiedSACunitDiameter(?sacd)





SACunitLengthMotorGearBoxMatchSatisfyRule:
SACunit(?sac) ^ hasSACunitDiameter(?sac, ?sacd) ^ hasSACunitLength(?sacd, ?sacl) ^ hasSACunitLengthInput(?sacl, ?sacli) ^ hasGearBox(?sacli, ?g) ^ hasDiameterInputDriveBearingMatch(?sacli, ?didbm) ^ hasSACunitDiameterInput(?didbm, ?sacdi) ^ hasMotor(?sacdi, ?m) ^
hasDimensionOuterDiameter_D_unit_mm(?sacd, ?D_res) ^ hasDimensionLength_L_unit_mm(?sacl, ?L_res)
SatisfiedMotorGearBoxMatch(?ma) ^ hasMotor(?ma, ?m) ^ hasGearBox(?ma, ?g) ^ hasMaximalSpeedRes_n_max_unit_rpm(?ma, ?n_res) ^ hasPeakTorqueRes_M_max_unit_Nm(?ma, ?M_res) ^ hasWeight_m_unit_kg(?ma, ?m_ges)
-> SatisfiedRotationSpeed(?sac) ^ hasMaximalSpeedRes_n_max_unit_rpm(?sac, ?n_res) ^ SatisfiedPeakTorque(?sac) ^ hasPeakTorqueRes_M_max_unit_Nm(?sac, ?M_res) ^ SatisfiedOuterDiameter(?sac) ^ hasDimensionOuterDiameter_D_unit_mm(?sac, ?D_res) ^ SatisfiedWeight(?sac) ^ hasWeight_m_unit_kg(?sac, ?m_ges) ^ SatisfiedLength(?sac) ^ hasDimensionLength_L_unit_mm(?sac, ?L_res)


SACunitSatisfyRule:
SACunit(?sac) ^ SatisfiedRotationSpeed(?sac) ^ SatisfiedPeakTorque(?sac) ^ SatisfiedLength(?sac) ^ SatisfiedOuterDiameter(?sac) ^ SatisfiedWeight(?sac) -> SatisfiedSACunit(?sac)



SACunitIsComposedOfRule:
SatisfiedSACunit(?sac) ^ hasSACunitDiameter(?sac, ?sacd) ^ hasSACunitLength(?sacd, ?sacl) ^ hasSACunitLengthInput(?sacl, ?sacli) ^ hasDiameterInputDriveBearingMatch(?sacli, ?didbm) ^ hasSACunitDiameterInput(?didbm, ?sacdi) ^ hasMotorController(?sacdi, ?mc) ^ hasIncrementalEncoder(?saclil, ?ie) ^ hasMotor(?sacdi, ?m) ^ hasGearBox(?sacli, ?g) ^ hasSACunitLengthOutput(?sacl, ?saclo) ^ hasSACunitDiameterOutputBearing(?saclo, ?sacdob) ^ hasSlipRingTorqueSensorMatch(?sacdob, ?srtsm) ^ hasSlipRing(?srtsm, ?sr) ^ hasTorqueSensor(?srtsm, ?ts) ^ hasDiameterOutputScrewMatch(?sacdob, ?gs) ^ hasScrew(?gs, ?s_gs) ^ hasSACunitDiameterOutput(?gs, ?sacdo) ^ hasDriveBearing(?didbm, ?db) ^ hasAbsoluteEncoderSensorPCBMatch(?sacdo, ?aespcbm) ^ hasOutputBearingScrewMatch(?sacdob, ?obs) ^  hasOutputBearing(?obs, ?ob) ^ hasScrew(?obs, ?s_ob) -> isComposedOfIncrementalEncoder(?sac, ?ie) ^ isComposedOfMotor(?sac, ?m) ^ isComposedOfGearBox(?sac, ?g) ^ GearBoxFlexScrew(?s_gs) ^  isComposedOfGearBoxFlexScrew(?sac, ?s_gs) ^ isComposedOfDriveBearing(?sac, ?db) ^ isComposedOfAbsoluteEncoderSensorPCB(?sac, ?aespcbm) ^ isComposedOfOutputBearing(?sac, ?ob) ^ OutputBearingScrew(?s_ob) ^ isComposedOfOutputBearingScrew(?sac, ?s_ob) ^ isComposedOfMotorController(?sac, ?mc) ^ isComposedOfSlipRing(?sac, ?sr) ^ isComposedOfTorqueSensor(?sac, ?ts)



